
;LAST UPDATE: 10.09.2013 savelij

;  
LOAD_FONT	LD HL,FONTS
		LD DE,CMP4FONTS
		JR FATBOOT4

; TAP 
EMULTAPLOAD	LD HL,TAPES
		LD DE,CMP4TAPES
		JR FATBOOT4

;  ROM    512KB
LOAD4FLASH_FULL	LD HL,ROMS
		LD DE,CMP4ROMS
		LD A,8
		LD (CMP_ROMSIZE),A
		JR FATBOOT4

; USER ROM    64KB
LOAD4FLASH_CUST	LD HL,ROMS
		LD DE,CMP4ROMS
		LD A,1
		LD (CMP_ROMSIZE),A
		JR FATBOOT4

;   
COPYIMAGE	LD HL,IMAGES
		LD DE,CMP4IMAGE
		JR FATBOOT4

; HOBETA   
FATBOOT		LD HL,HOBETA
		LD DE,CMP4HOBETA
FATBOOT4	LD (GDE_EXT_FILES),HL
		LD (CMP_FOUNDED),DE
SELECT_DEVICE	DI
		LD HL,FLAGS
		SET 3,(HL)			;   FAT
;		RST 8
;		DB _COM_DEV			;  
;		DB _DEVFIND			; 
;		JP C,_STUPID			;,     
		RST 8
		DB _COM_DEV
		DB _KOL_VOL
		LD A,E
		AND A
		JP Z,_STUPID
		LD HL,ADR_CAT-0X100		;     
		RST 8
		DB _COM_DEV			;  
		DB _GET_FNDVOLUME		;   
		LD B,E				;   
		LD IX,SEL_FAT_DEV		;    
		LD (IX+7),D			;   
		LD (IX+8),D			;  
		LD (IX+0X0A),E			;   
		INC E
		INC E
		LD (IX+2),E			;   
		LD DE,6				;     -2
		EXX
		LD HL,TXT_FAT_SEL
		LD DE,SEL_FAT_DRV		;       
		LD BC,ETXT_FAT_SEL-TXT_FAT_SEL
		LDIR
		EX DE,HL
		EXX
FATBOOT1	EXX
		LD (HL),0X0D
		INC HL
		LD (HL),3
		INC HL
		EXX
		LD A,(HL)			;  
		INC HL
		LD C,(HL)			;MASTER  SLAVE
		INC HL
		ADD A,C
		ADD A,C
		SUB 4				;-4,    
		ADD A,A
		ADD A,A
		ADD A,A
		ADD A,A				;  16
		EXX
		EX DE,HL
		LD HL,TXT4MENU_DEVICE		;    
		LD C,A
		LD B,0
		ADD HL,BC			;    
		LD BC,0X10			;  16 
		EXX
		LD A,L
		EXX
		RRCA
		RRCA
		RRCA
		AND 0X1F
		ADD A,"E"			;  
		LD (DE),A
		INC DE
		LDIR				;    
		EXX
		LD A,(HL)			; 
		EXX
		LD C,A
		ADD A,A
		ADD A,A
		ADD A,C				;  5
		LD HL,TXT4MENU_TYPE		;    
		LD C,A
		ADD HL,BC
		LD C,5
		LDIR				;    
		EX DE,HL
		EXX
		ADD HL,DE
		DJNZ FATBOOT1
		EXX
		LD (HL),0
		INC HL
		LD B,(IX+2)
		DEC B
		DEC B
		LD C,B
		LD A,"E"
		LD (IX+0X12),L
		LD (IX+0X13),H			;     FAT 
		LD (HL),B
		INC HL
FATBOOT3	LD (HL),A			;   
		INC HL
		INC A
		DJNZ FATBOOT3
		LD (SELFATDEVEXE),HL		;     
		LD B,C
FATBOOT2	LD (HL),LOW (SEL_FAT)		;   
		INC HL
		LD (HL),HIGH (SEL_FAT)
		INC HL
		DJNZ FATBOOT2
		LD A,C
		EXX
		DEC A
		JR Z,SEL_FAT
		RST 8
		DB _WINW
		JP _RULILKA			;   

FILES_BROWSE	LD HL,FILES_EXT
		LD DE,CMP4TAPES
		DI
		LD (GDE_EXT_FILES),HL
		LD (CMP_FOUNDED),DE
		LD HL,FLAGS
		SET 3,(HL)
		JR SEL_FILES

;       
SEL_FAT		DI
		LD A,(IX+8)			;   
		RST 8
		DB _COM_DEV
		DB _SET_VOL			;   
SEL_FILES	RST 8
		DB _COM_DEV
		DB _KOL_VOL
		LD A,E
		AND A
		JP Z,_STUPID
		CALL SEARCH_FILE
		LD HL,1
		AND A
		SBC HL,BC			; - ?
		JR NZ,SEL_FAT1			;  >1,  
		PUSH BC				;  
		LD BC,(ADR_CAT)			;   
		LD A,6
		RST 8
		DB _COM_FAT
		DB _POS_FILES
		LD HL,BUF_FILEITEM
		RST 8
		DB _COM_FAT
		DB _READ_DIR			; 
		POP BC				;  
		LD DE,0X0B
		ADD HL,DE
		LD A,(HL)			;     
		AND 0X10
		LD HL,0				; ,   0  
		JR Z,RUN_HOB1			;    
SEL_FAT1	LD IX,FAT_FILES			; ,    
		JP OUT_HOB

;     HOBETA   /  
RUN_HOB		DI
		CALL SHUT2AY			;  AY
		LD L,(IX+8)
		LD H,(IX+9)			;HL=  
RUN_HOB1	LD DE,ADR_CAT
		ADD HL,HL
		ADD HL,DE			;   
		LD C,(HL)
		INC HL
		LD B,(HL)			;   
		LD A,6
		RST 8
		DB _COM_FAT
		DB _POS_FILES			;   FAT 
		LD HL,BUF_FILEITEM
		RST 8
		DB _COM_FAT
		DB _READ_DIR			;    
		LD DE,0X0B			;   
		ADD HL,DE			;    
		LD A,(HL)			;     
		SBC HL,DE
		AND 0X10			;
		JP Z,GO_RUN_HOB			;  ,    
		RST 8
		DB _COM_FAT
		DB _ENTER_DIR			;   
		PUSH HL				;      
		LD HL,BUF_RDPATH
		RST 8
		DB _COM_FAT
		DB _GET_PATH			;       
		LD A,(HL)			;    
		SUB "E"				;   
		LD D,A
		LD E,0
		LD HL,BUF_LEVDIR
		SRL D
		RR E
		SRL D
		RR E
		ADD HL,DE
		EX (SP),HL			;   FAT ,    
		LD A,(HL)			;      
		POP HL				;   FAT  
		CP "."				;     ".",        
		JR NZ,SNYT0			;  
		LD C,(HL)			;       
		INC HL
		LD B,(HL)
		INC HL
		PUSH HL
		ADD HL,BC
		DEC HL
		LD A,(HL)
		DEC HL
		LD C,(HL)
		DEC HL
		LD B,(HL)
		POP DE
		AND A
		SBC HL,DE
		EX DE,HL
		DEC HL
		LD (HL),D
		DEC HL
		LD (HL),E
		LD (IX+8),C
		LD (IX+9),B
		LD (IX+7),A
		CALL SEARCH_FILE		;       
		LD (IX+0X0A),C
		LD (IX+0X0B),B			;        
		JP REOUT_HOB			;   

;       
SNYT0		LD C,(HL)
		INC HL
		LD B,(HL)			;BC=    
		INC HL
		PUSH HL				;HL=   
		ADD HL,BC			;HL=   
		LD D,(IX+9)
		LD E,(IX+8)
		LD A,(IX+7)
		LD (HL),D
		INC HL
		LD (HL),E
		INC HL
		LD (HL),A
		INC HL
		POP DE				;DE=   
		AND A
		SBC HL,DE
		EX DE,HL
		DEC HL
		LD (HL),D
		DEC HL
		LD (HL),E
		LD C,(IX+0X0A)
		LD B,(IX+0X0B)
		CALL SEARCH_FILE
		JP OUT_HOB

SELMOUNT	LD IX,SEL_MOUNT
		RST 8
		DB _WINW
		JP _RULILKA

MOUNT_A		XOR A
MOUNT_ALL	RST 8
		DB _MOUNTER
		DB _OPEN_MOUNT
		JP RESTART

MOUNT_B		LD A,1
		JR MOUNT_ALL

MOUNT_C		LD A,2
		JR MOUNT_ALL

MOUNT_D		LD A,3
		JR MOUNT_ALL

;,    
GO_RUN_HOB	RST 8
		DB _COM_FAT
		DB _OPEN_FILE
		LD DE,8
		ADD HL,DE
		PUSH HL
		LD HL,(BUF_FILEITEM+8)
		LD DE,"RT"
		AND A
		SBC HL,DE
		POP HL
		JR Z,SELMOUNT
		LD A,(HL)
		CP "$"
		JR Z,GORUNHOB1			;   HOBETA
		CP "S"
		JP NZ,LOAD_IMAGE		;   
		INC HL
		LD A,(HL)
		CP "P"
		DEC HL
		LD A,(HL)
		JP NZ,LOAD_IMAGE		;   
GORUNHOB1	RST 8
		DB _COM_DEV
		DB _KOL_VOL			;      
		LD A,D				;   
		LD HL,ADR_CAT
		RST 8
		DB _COM_DEV
		DB _GET_FNDVOLUME
		ADD A,A
		ADD A,A
		ADD A,A
		LD C,A
		LD B,0
		ADD HL,BC
		LD A,(HL)			;  
		SUB 4				; 
		LD D,A				;  
		INC HL
		LD E,(HL)			;   (MASTER/SLAVE)
		PUSH DE
		LD HL,0X5800
		LD DE,0X5801
		LD BC,0X02FF
		LD (HL),L
		LDIR				;    
		LD HL,RUN_CODE
		LD DE,0X5D3B
		LD BC,ERUN_CODE-RUN_CODE
		LDIR
		CALL MEMSET			;  
		CALL FOR_RUN
		LD HL,0X4200			;   FAT   
		RST 8
		DB _INST_FATBOOT
		POP DE
		LD (0X42FE),DE
		JP 0X4300

;   
LOAD_FNT	LD HL,ADR_CAT
LOAD_FNT1	LD A,4
		RST 8
		DB _COM_FAT
		DB _READ_FILE
		PEC_ON FONT_BF
		LD HL,ADR_CAT
		LD DE,0
		LD BC,0X800
		LDIR
		PEC_OFF FONT_BF
		JP RESTART

; FDI 
LOAD_FDI	LD A,E
		CP "N"
		JR Z,LOAD_FNT			;   
		LD A,PAGE_RAMDISK		;  
		PUSH AF				;  
		CALL SET4MBPAGE			; 
		LD HL,FDI_TABLE_CYL
		LD DE,FDI_TABLE_CYL+1
		LD BC,0X3FFF
		LD (HL),0
		LDIR				;    
		LD IXH,0XFF
		CALL LOAD1SECFDI		;   
		LD A,(0XC006)			;  
		DEC A
		LD A,(0XC004)			;  
		JR Z,LOADFDI1
		ADD A,A				;  2,      2
LOADFDI1	LD C,A				; 
		LD HL,(0XC00A)
		LD (START_DATA),HL
		LD DE,0X4000			;    
		LD HL,0XC00F			;      FDI 
LOADFDI3	BIT 1,H
		CALL NZ,LOAD1SECFDI		;            
		LD A,(HL)			; 8-15    
		AND 0X3F			;  8-13 -    
		LD (DE),A			;     
		LD A,(HL)			;  14-15
		INC HL				;  FDI 
		INC D				;       
		BIT 1,H
		CALL NZ,LOAD1SECFDI		;            
		LD B,A				;  14-15  
		LD A,(HL)			; 16-23     
		RL B
		RLA
		RL B
		RLA
		LD (DE),A			;     
		INC D
		REPT 4
		INC HL
		ENDM
		BIT 1,H
		CALL NZ,LOAD1SECFDI		;            
		LD B,(HL)			;    
		LD A,B
		AND A
		JP Z,LOADFDI_ERR
		INC HL
		INC HL
LOADFDI4	INC HL
		BIT 1,H
		CALL NZ,LOAD1SECFDI		;            
		LDI				;  
		DEC DE
		INC C
		INC D
		BIT 1,H
		CALL NZ,LOAD1SECFDI		;            
		LD A,(HL)			;  
		EX DE,HL
		LD (HL),1
		AND A
		JR Z,LOADFDI2
		LD (HL),2
		DEC A
		JR Z,LOADFDI2
		LD (HL),4
		DEC A
		JR Z,LOADFDI2
		LD (HL),8
LOADFDI2	EX DE,HL
		INC D
		REPT 5
		INC HL
		ENDM
		DJNZ LOADFDI4			;  ,     
		LD D,0X40
		INC E
		DEC C
		JR NZ,LOADFDI3			;      
		DEC HL
		BIT 1,H
		CALL NZ,LOAD1SECFDI		;            
		PUSH HL
		LD A,H
		AND 1
		LD B,A
		LD C,L
		LD A,IXH
		ADD A,A
		ADD A,B
		LD B,A
		LD HL,0
START_DATA	EQU $-2
		AND A
		SBC HL,BC
		EX DE,HL
		POP HL
		LD A,D
		AND A
		JR Z,LOADFDI7
LOADFDI8	INC H
		BIT 1,H
		CALL NZ,LOAD1SECFDI		;            
		DEC D
		JR NZ,LOADFDI8
LOADFDI7	ADD HL,DE
		BIT 1,H
		CALL NZ,LOAD1SECFDI		;            
		PUSH HL				;     
		LD DE,0XC000
		AND A
		SBC HL,DE
		LD (OSTAT_SECT),HL		;          
		LD B,H
		LD C,L
		LD HL,0X200
		SBC HL,BC
		LD (NACH_SECT),HL		;          
		POP HL				;   ,      
LOADFDI5	POP AF
		INC A
		CALL SET4MBPAGE
		PUSH AF
		BIT 1,H
		CALL NZ,LOAD1SECFDI
		EX AF,AF'
		LD DE,0X4000
		LD BC,0
NACH_SECT	EQU $-2
		LDIR
		EX AF,AF'
		JP C,ELOAD_IMAGE
		EX DE,HL
		LD A,0X1F
		RST 8
		DB _COM_FAT
		DB _READ_FILE
		JP C,ELOAD_IMAGE
		PUSH HL
		LD HL,0XC000
		PUSH HL
		LD A,1
		RST 8
		DB _COM_FAT
		DB _READ_FILE
		POP HL
		POP DE
		LD BC,0
OSTAT_SECT	EQU $-2
		LDIR
		JR LOADFDI5

LOADFDI_ERR	LD A,PAGE_RAMDISK
		CALL SET4MBPAGE
		LD HL,0X7FFF
		LD (HL),0
		DEC H
		LD (HL),0
		LD A,0XFA
		CALL SET4MBPAGE
		LD SP,(LD_FILE_SP)
		POP IY
		POP IX
		JP LDFDI_ERROR

;     
CREATE_TRD640	PUSH IX
		PUSH IY
		LD (LD_FILE_SP),SP
		LD SP,ADR_CAT
		JR ELT4

CREATE_RAMTABL	PUSH IX
		PUSH IY
		LD (LD_FILE_SP),SP
		LD SP,ADR_CAT
;     TRD  SCL 
END_LOAD_TRD	LD A,PAGE_DATARAMD		;     
		CALL SET4MBPAGE
		LD HL,(0X48E1)			;   
		LD A,L
		LD L,H
		LD H,0
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		OR L
		LD L,A				;    
		LD DE,(0X48E5)			;  
		ADD HL,DE
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL			;H= 
		LD A,H
		CP 0XA0
		JR NC,ELT3
ELT4		LD A,0XA0
ELT3		EX AF,AF'
		LD A,PAGE_RAMDISK		;   ,    
		CALL SET4MBPAGE
		LD HL,0X4000
		PUSH HL
		LD DE,0X4001
		LD BC,0X3FFF
		LD (HL),L
		LDIR				; 
		POP DE				;  
		LD HL,0				;    
		EX AF,AF'
ELT2		EX AF,AF'
		LD BC,0X1000			;     
		LD A,L
		RRCA
		RRCA
		LD (DE),A			;     
		INC D
		LD A,H
		LD (DE),A			;    
		INC D
ELT1		INC C
		LD A,C
		LD (DE),A			; 
		INC D
		LD A,2
		LD (DE),A			; 
		INC D
		DJNZ ELT1			;       
		LD D,0X40			;   
		INC E				;  
		LD BC,0X40
		ADD HL,BC			;   
		EX AF,AF'
		DEC A
		JR NZ,ELT2			;   
;           1
ELOAD_IMAGE	LD A,PAGE_RAMDISK
		CALL SET4MBPAGE
		LD HL,0X7FFF
		LD (HL),"R"
		DEC H
		LD (HL),"D"
		LD A,0XFA
		CALL SET4MBPAGE
		LD SP,0
LD_FILE_SP	EQU $-2
		POP IY
		POP IX
		RET

;    FDI
LOAD1SECFDI	PUSH HL
		PUSH BC
		PUSH DE
		PUSH AF
		LD HL,0XC000
		LD A,1
		RST 8
		DB _COM_FAT
		DB _READ_FILE
		INC IXH
		POP AF
		POP DE
		POP BC
		POP HL
		RES 1,H
		RET

;  1    
SET4MBPAGE	PUSH BC
		LD B,A
		PEC_ON SHADOW_BF
		LD A,B
		LD BC,WIN_P1
		OUT (C),A
		LD B,A
		PEC_OFF SHADOW_BF
		LD A,B
		POP BC
		RET

LOADIMAGE4	PUSH IX
		PUSH IY
		LD (LD_FILE_SP),SP
		LD SP,ADR_CAT
		LD A,PAGE4TAP
; TRD  TAP 
LOADIMAGE3	PUSH AF
		CALL SET4MBPAGE
		LD HL,0X4000
		LD A,0X20
		RST 8
		DB _COM_FAT
		DB _READ_FILE
		JR C,END_LOAD_TAP
		POP AF
		INC A
		JR LOADIMAGE3

END_LOAD_TAP	XOR A
		CALL SET4MBPAGE
		LD H,A
		LD L,A
		LD (0X4000),HL
		LD (0X4002),A
		LD A,0XFA
		CALL SET4MBPAGE
		LD SP,(LD_FILE_SP)
		POP IY
		POP IX
		LD H,CMOS_BYTE_00
		CALL READCMOS
		OR EMUL_TAPE
		LD L,A
		CALL WRITECMOS
		JP TAPELOAD1

LOADIMAGE	LD DE,"TR"
		JR LOADIMAGE2

LOAD_IMAGE	DI
		LD D,A				;  
		INC HL
		LD E,(HL)			;  
		CP "B"
		JR NZ,LOADIMAGE1_
		LD A,E
		CP "M"
		JR Z,LOADIMAGE2			;BMP
LOADIMAGE1_	CP "T"				;TAP  TRD ?
		JR NZ,LOADIMAGE1
		LD A,E
		CP "A"
		JR Z,LOADIMAGE4			;TAP
LOADIMAGE1	LD A,D
		CP "R"
		LD HL,FLASHER
		JR Z,LOAD_IMAGE8		;TRD
LOADIMAGE2	LD HL,RESTART
LOAD_IMAGE8	PUSH HL
		PUSH IX
		PUSH IY
		LD (LD_FILE_SP),SP
		LD SP,ADR_CAT
		LD A,D
		CP "S"				; SCL ?
		JR Z,LOAD_IMAGE2
		CP "F"				; FDI ?
		JP Z,LOAD_FDI
		CP "B"				; BMP ?
		JP Z,BMPVIEW
		CP "T"				; TRD ?
		JR NZ,LOAD_ROM
		LD A,PAGE_DATARAMD
; TRD  TAP 
LOAD_IMAGE1	PUSH AF
		CALL SET4MBPAGE
		LD HL,CPU1
		LD A,0X20
		RST 8
		DB _COM_FAT
		DB _READ_FILE
		JP C,END_LOAD_TRD
		POP AF
		INC A
		JR LOAD_IMAGE1

LOAD_ROM	CP "R"				; ROM ?
		JP NZ,ELOAD_IMAGE
		LD A,PAGE4FLASHER
		CALL LOAD_IN_PAGE
		LD SP,(LD_FILE_SP)
		POP IY
		POP IX
		RET

LOAD_IN_PAGE	PUSH AF
		CALL SET4MBPAGE
		LD HL,CPU1
		LD A,0X20
		RST 8
		DB _COM_FAT
		DB _READ_FILE
		JR C,LOADINPAGE1
		POP AF
		INC A
		JR LOAD_IN_PAGE

LOADINPAGE1	POP AF
		LD A,0XFA
		JP SET4MBPAGE

; SCL 
LOAD_IMAGE2	LD HL,DSKINFO
		LD DE,ADR_CAT+0XF00
		LD BC,DSK_END-DSKINFO
		LDIR				;     
		LD A,PAGE_DATARAMD
LOAD_IMAGE4	PUSH AF
		CALL SET4MBPAGE			;     
		LD HL,0X5000
		LD A,0X18
		RST 8
		DB _COM_FAT
		DB _READ_FILE
		PUSH AF				;       
		LD HL,0X4000
		PUSH HL				;   
		LD (HL),L
		LD D,H
		LD E,L
		INC DE
		LD BC,0XFFF
		LDIR				;       
		LD HL,0X5008			;    SCL 
		LD A,(HL)			;    
		INC HL				;    
		EXX
		LD HL,0				;  
		LD D,L				;      255 
		LD B,A				;  
		EXX
		POP DE				;    
LOAD_IMAGE3	EX AF,AF'
		LD BC,0X0D
		LDIR				; 13   
		LD A,(HL)			;    
		LDI				;  
		EXX
		LD E,A
		PUSH HL				;    
		ADD HL,DE			;       
		EXX
		EX (SP),HL
		LD A,L
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		INC H
		AND 0X0F
		LD L,A
		EX (SP),HL
		POP BC				;   
		EX DE,HL
		LD (HL),C
		INC HL
		LD (HL),B			;   
		INC HL
		EX DE,HL
		EX AF,AF'
		DEC A
		JR NZ,LOAD_IMAGE3		;    
		PUSH HL				;   
		LD HL,ADR_CAT+0XF00
		LD DE,0X48E1
		LD BC,DSK_END-DSKINFO
		LDIR
		EXX
		LD DE,2544			;  
		EX DE,HL
		AND A
		SBC HL,DE
		LD (0X48E5),HL			;  
		EX DE,HL
		LD A,L
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		INC H
		AND 0X0F
		LD L,A
		LD (0X48E1),HL			;    
		LD A,B
		LD (0X48E4),A			;   
		EXX
		POP DE				;   
		LD HL,0X8000			;  
		AND A
		SBC HL,DE			;    
		LD B,H
		LD C,L
		LD HL,0X5000			;   
		EX DE,HL
		LDIR				;,   DE=   
		LD A,D
		CP 0X7E
		JR NC,LOAD_IMAGE7
		EX DE,HL
		LD A,0X7F
		SUB H
		SRL A
		LD IXL,A
		RST 8
		DB _COM_FAT
		DB _READ_FILE
		LD DE,0X8000
		EX DE,HL
LOAD_IMAGE7	AND A
		SBC HL,DE			;      
		LD (OST_HWOST),HL
		LD (KUDA_HWOST),DE
		LD DE,0
		EX DE,HL
		SBC HL,DE
		LD A,1
		AND H
		LD H,A
		LD (NEXT4PAGE),HL
		POP AF				;   
		JP C,END_LOAD_TRD		;   ,  
		JR LOAD_IMAGE5

LOAD_IMAGE6	LD DE,0X4000
		LD BC,0
NEXT4PAGE	EQU $-2
		LDIR
		EX AF,AF'
		JP C,END_LOAD_TRD
		EX DE,HL
		LD A,0X1F
		RST 8
		DB _COM_FAT
		DB _READ_FILE
		JP C,END_LOAD_TRD
LOAD_IMAGE5	LD HL,ADR_CAT
		PUSH HL
		LD A,1
		RST 8
		DB _COM_FAT
		DB _READ_FILE
		EX AF,AF'			;   
		POP HL				;      
		LD DE,0
KUDA_HWOST	EQU $-2				;    
		LD BC,0
OST_HWOST	EQU $-2				;    
		LDIR
		POP AF
		INC A
		PUSH AF
		CALL SET4MBPAGE
		JR LOAD_IMAGE6

;  HOBETA 
; :  =1- 
CMP4HOBETA	SUB 1
		RET NZ
		LD A,(BUF_RDSEC+8)
		CP "C"				;    ,   - 
		SCF
		RET NZ
		PUSH DE				;     
		LD BC,BUF_RDSEC			;   
		CALL HOB_CRC			; CRC HOBETA 
		LD A,(BC)
		INC BC
		LD E,A
		LD A,(BC)
		LD D,A				; "DE"  CRC  
		AND A				; "HL"    +0X0F   
		SBC HL,DE			; 
		POP DE				;       
		SCF
		RET NZ				; 
		EXX
		LD HL,(BUF_RDSEC+9)
		DEC HL				;   40960  (160 )
		LD DE,(BUF_RDSEC+0X0B)
		ADD HL,DE			;    
		EXX				;    
		RET C				; 
		EXX
		LD HL,(BUF_RDSEC+9)
		LD DE,0X6000			;  
		AND A				;      0X6000
		SBC HL,DE
		EXX				;    0X6000
		RET

; : BC- 
; : HL-HOBETA CRC
HOB_CRC		LD A,0X0F
		LD HL,0
		EXX
		LD E,0
		EXX
HOBCRC1		EX AF,AF'
		LD A,(BC)
		LD E,A
		LD D,A
		ADD HL,DE
		EXX
		LD A,E
		INC E
		EXX
		LD E,A
		LD D,0
		ADD HL,DE
		EX AF,AF'
		INC BC
		DEC A
		JR NZ,HOBCRC1
		RET

CMP4FONTS	LD DE,(BUF_FILEITEM+0X1C)
		LD HL,(BUF_FILEITEM+0X1E)
		LD A,H
		OR L
		OR E
		JR NZ,CMP4IMAGE2
		LD A,D
		CP 8
		JR NZ,CMP4IMAGE2
		AND A
		RET

CMP4ROMS	LD DE,(BUF_FILEITEM+0X1C)
		LD HL,(BUF_FILEITEM+0X1E)
		LD A,H
		OR D
		OR E
		JR NZ,CMP4IMAGE2
		LD A,L
		CP 8
CMP_ROMSIZE	EQU $-1
		JR NZ,CMP4IMAGE2
CMP4ROMS1	AND A
		RET

CMP4IMAGE2	SCF
		RET

CMP4FDI		EXX
		LD HL,BUF_RDSEC
		LD A,(HL)
		CP "F"
		JR NZ,CMP4IMAGE2
		INC HL
		LD A,(HL)
		CP "D"
		JR NZ,CMP4IMAGE2
		INC HL
		LD A,(HL)
		CP "I"
		JR NZ,CMP4IMAGE2
		INC HL
		INC HL
		LD A,(HL)			; 
		CP 0X57
		JR NC,CMP4IMAGE2
		INC HL
		LD A,(HL)			;  0
		AND A
		JR NZ,CMP4IMAGE2
		INC HL
		LD A,(HL)			; 
		AND A
		JR Z,CMP4IMAGE2
		CP 3
		JR NC,CMP4IMAGE2
		INC HL
		LD A,(HL)			;  0
		AND A
		JR NZ,CMP4IMAGE2
		EXX
		AND A
		RET

CMP4IMAGE	CP 3
		JR Z,CMP4FDI
		CP 4
		JR Z,CMP4BMP
		SUB 1
		RET Z				; TRD   
; SCL    "SINCLAIR"   8 
		EXX
		LD HL,BUF_RDSEC
		LD DE,SIGN_SCL
		LD B,8
CMP4IMAGE1	LD A,(DE)
		CP (HL)
		JR NZ,CMP4IMAGE2
		INC DE
		INC HL
		DJNZ CMP4IMAGE1
		EXX
		AND A
CMP4TAPES	RET

CMP4BMP		EXX
		LD HL,BUF_RDSEC
		LD A,"B"
		SUB (HL)
		SCF
		RET NZ
		INC HL
		LD A,"M"
		SUB (HL)
		SCF
		RET NZ
		LD HL,(BUF_RDSEC+0X0A)
		LD DE,0X76
		AND A
		SBC HL,DE
		SCF
		RET NZ
		LD HL,(BUF_RDSEC+0X12)
		LD DE,0X140
		AND A
		SBC HL,DE
		SCF
		RET NZ
		LD HL,(BUF_RDSEC+0X16)
		LD DE,0XC8
		AND A
		SBC HL,DE
		SCF
		RET NZ
		LD A,(BUF_RDSEC+0X1C)
		CP 4
		SCF
		RET NZ
		EXX
		AND A
		RET

SEARCH_FILE	LD A,1
		LD HL,(GDE_EXT_FILES)
		RST 8
		DB _COM_FAT
		DB _SET_MASK_EXT
		LD DE,ADR_CAT
		RST 8
		DB _COM_FAT
		DB _FIND_FILEITEM
		LD H,A				;    
		LD A,B
		OR C
		JR NZ,SEARCHF1
		PUSH IX
		LD IX,NOBASIC
		RST 8
		DB _WINW
		POP IX
		RST 8
		DB _COM_DEV
		DB _KOL_VOL
		DEC E
		JP Z,NO_BAS
		JP SELECT_DEVICE

SEARCHF1	PUSH BC
		PUSH HL
		RST 8
		DB _SORT_FINDFILES
		PEC_ON SHADOW_BF
		POP AF
		LD BC,WIN_P1
		OUT (C),A
		LD HL,CPU1
		LD DE,CPU3+0X1000
		LD BC,0X1000
		LDIR
		LD BC,WIN_A1
		LD A,0X7A
		OUT (C),A
		PEC_OFF SHADOW_BF
		POP BC
		LD DE,ADR_CAT
		LD HL,ADR_CAT+0X1000
SEARCHF3	PUSH BC
		LD C,(HL)
		INC HL
		LD B,(HL)
		DEC HL
		LD A,6
		RST 8
		DB _COM_FAT
		DB _POS_FILES
		PUSH HL
		LD HL,BUF_FILEITEM
		RST 8
		DB _COM_FAT
		DB _READ_DIR
		PUSH DE
		LD DE,0X0B
		ADD HL,DE
		LD A,(HL)
		AND 0X10
		JR NZ,SEARCHF4
		RST 8
		DB _COM_FAT
		DB _OPEN_FILE
		LD HL,BUF_RDSEC
		LD A,1
		RST 8
		DB _COM_FAT
		DB _READ_FILE
		LD HL,BUF_FILEITEM
		CALL CP_EXT
		CALL 0
CMP_FOUNDED	EQU $-2
SEARCHF4	POP DE
		POP HL
		JR NC,SEARCHF5
		INC HL
		INC HL
		JR SEARCH6

SEARCHF5	LDI
		LDI
SEARCH6		POP BC
		DEC BC
		LD A,B
		OR C
		JR NZ,SEARCHF3
		LD HL,ADR_CAT
		EX DE,HL
		SBC HL,DE
		SRL H
		RR L
		LD B,H
		LD C,L
		RET

CP_EXT		PUSH BC
		PUSH DE
		PUSH HL
		LD C,0
		LD DE,0
GDE_EXT_FILES	EQU $-2
CPETR2		LD A,(DE)
		AND A
		JR Z,CPETR1+1
		INC C
		PUSH DE
		PUSH HL
		CALL COMPARF
		POP HL
		POP DE
		JR Z,CPETR1
		INC DE
		INC DE
		INC DE
		JR CPETR2

CPETR1		LD A,C
		AND A
		POP HL
		POP DE
		POP BC
		RET

COMPARF		PUSH DE
		LD DE,8
		ADD HL,DE
		POP DE
COMPARF1	LD A,(DE)
		CP (HL)
		RET NZ
		INC HL
		INC DE
		LD A,(DE)
		CP (HL)
		RET NZ
		INC HL
		INC DE
		LD A,(DE)
		CP (HL)
		RET

BMPVIEW		LD A,PAGE4FLASHER
		CALL LOAD_IN_PAGE
		LD A,PAGE4FLASHER
		CALL SET4MBPAGE
		CALL CREATE_PAL
		CALL SET_COLORS
		LD A,1
		LD HL,SCR_PAL
		RST 8
		DB _SETUP_PAL
		CALL OUT_SCREEN
		XOR A
		RST 8
		DB _SETUP_PAL
		LD SP,(LD_FILE_SP)
		POP IY
		POP IX
		RET

BUF_COLORS	EQU 0XB800

OUT_SCREEN	LD A,PAGE4FLASHER
		CALL SET4MBPAGE
		PEC_ON SHADOW_BF
		LD BC,0XFF77
		LD A,0XA8
		OUT (C),A
		LD BC,CONF_128
		LD HL,CPU3+0X1F18
		EXX
		LD DE,CPU1
		LD HL,(CPU1+0X0A)
		ADD HL,DE
		LD A,200
COPYPIC2	EX AF,AF'
		LD E,0X28
COPYPIC1	OUT (C),D
		LD A,(HL)	;0
		INC HL
		EXX
		LD D,0X1B
		OUT (C),D
		LD D,HIGH (BUF_COLORS)
		LD E,A
		LD A,(DE)
		LD (HL),A	;7 6
		EXX
		OUT (C),D
		LD A,(HL)	;1
		INC HL
		EXX
		LD D,0X1F
		OUT (C),D
		LD D,HIGH (BUF_COLORS)
		LD E,A
		LD A,(DE)
		LD (HL),A	;5 4
		LD A,0X20
		XOR H
		LD H,A
		EXX
		OUT (C),D
		BIT 7,H
		JP Z,COPYPIC4
		LD HL,CPU1
		LD A,PAGE4FLASHER+1
		CALL SET4MBPAGE
		PEC_ON SHADOW_BF
COPYPIC4	LD A,(HL)	;2
		INC HL
		EXX
		LD D,0X1B
		OUT (C),D
		LD D,HIGH (BUF_COLORS)
		LD E,A
		LD A,(DE)
		LD (HL),A	;3 2
		EXX
		OUT (C),D
		LD A,(HL)	;3
		INC HL
		EXX
		LD D,0X1F
		OUT (C),D
		LD D,HIGH (BUF_COLORS)
		LD E,A
		LD A,(DE)
		LD (HL),A	;1 0
		LD A,0X20
		XOR H
		LD H,A
		INC HL
		EXX
COPYPIC3	DEC E
		JP NZ,COPYPIC1
		EXX
		PUSH DE
		LD DE,-0X50
		ADD HL,DE
		POP DE
		EXX
		EX AF,AF'
		DEC A
		JP NZ,COPYPIC2
		XOR A
		IN A,(0XFE)
		CPL
		AND 0X1F
		JR Z,$-6
		XOR A
		IN A,(0XFE)
		CPL
		AND 0X1F
		JR NZ,$-6
		LD BC,0XFF77
		LD A,0XA3
		OUT (C),A
		LD BC,CONF_128
		LD A,0X10
		OUT (C),A
		PEC_OFF SHADOW_BF
		LD A,0XFA
		JP SET4MBPAGE

SCR_PAL		DUPL 0X10,0

BLUE_3		DB 0,B_L,B_H,B_L|B_H
GREEN_3		DB 0,G_L,G_H,G_L|G_H
RED_3		DB 0,R_L,R_H,R_L|R_H

SET_COLORS	LD HL,BUF_COLORS
SETCOLORS1	LD E,L
		LD A,E
		AND 7
		RLCA
		RLCA
		RLCA
		LD D,A
		LD A,E
		AND 8
		RLCA
		RLCA
		RLCA
		RLCA
		OR D
		LD D,A
		LD A,E
		AND 0X70
		RRCA
		RRCA
		RRCA
		RRCA
		OR D
		LD D,A
		LD A,E
		AND 0X80
		RRCA
		OR D
		LD (HL),A
		INC L
		JR NZ,SETCOLORS1
		RET

CREATE_PAL	LD DE,CPU1+0X36
		LD HL,SCR_PAL+0X0F
		LD A,0X10
		EXX
		LD D,0
		EXX
SETPAL1		EX AF,AF'
		LD A,(DE)
		AND 0XC0
		RLCA
		RLCA
		EXX
		LD HL,BLUE_3
		LD E,A
		ADD HL,DE
		LD A,(HL)
		EXX
		LD B,A		;BLUE
		INC DE
		LD A,(DE)
		AND 0XC0
		RLCA
		RLCA
		EXX
		LD HL,GREEN_3
		LD E,A
		ADD HL,DE
		LD A,(HL)
		EXX
		OR B
		LD B,A		;GREEN
		INC DE
		LD A,(DE)
		AND 0XC0
		RLCA
		RLCA
		EXX
		LD HL,RED_3
		LD E,A
		ADD HL,DE
		LD A,(HL)
		EXX
		OR B
		CPL
		LD (HL),A
		DEC HL
		INC DE
		INC DE
		EX AF,AF'
		DEC A
		JP NZ,SETPAL1
		RET

;    
FLASHER		LD BC,PENT_CONF
		XOR A
		OUT (C),A
		PEC_ON SHADOW_BF
		LD BC,0XFF77
		LD A,0XA3
		OUT (C),A			;    7
		LD A,(CMP_ROMSIZE)
		DEC A
		JR NZ,FLASHER6
		LD IX,UPDATE_CUSTOM
		RST 8
		DB _WINW
		LD SP,ADR_CAT
		PEC_ON SHADOW_BF+FLASH_BF
		LD BC,WIN_A0
		LD A,0X7F
		OUT (C),A			;    0
		LD E,0
		LD HL,0
		CALL ERASE_BLK			; COSTOM ROM 
		LD A,PAGE4FLASHER
		LD E,0
		CALL FLASH_64KB			; CUSTOM ROM 
		JP EFLASHER

FLASHER6	LD IX,ERASE_ROM			;    FLASH
		RST 8
		DB _WINW
		LD IX,UPDATE_WIN		;    FLASH
		RST 8
		DB _WINW
		LD IX,ERASE_ROM
		LD SP,ADR_CAT
		PEC_ON SHADOW_BF+FLASH_BF	;  FLASH
		LD BC,WIN_A0
		LD A,0X7F
		OUT (C),A			; ROM   0
		LD A,3
		CALL SETWIN_INDICAT		;  
		LD E,0
		LD HL,0
FLASHER4	CALL ERASE_BLK			;  64
		CALL INC_INDICAT		;  
		INC E				; 	
		BIT 3,E
		JR Z,FLASHER4
		LD IX,UPDATE_WIN
		LD A,PAGE4FLASHER		;   ROM 
		LD E,0
FLASHER3	CALL FLASH_64KB			;  64 
		INC E				; 64 
		BIT 3,E
		JR Z,FLASHER3
EFLASHER	LD BC,WIN_P3
		XOR A
		OUT (C),A			; RAM   3
		LD A,(CPU3+0X20)
		XOR B
		LD (CPU3+0X20),A		;    CRC    
		LD A,(CPU1+BUF_TABLVOL+0XFE)
		XOR B
		LD (CPU1+BUF_TABLVOL+0XFE),A	;  DEVICE MANAGER
		PEC_OFF FLASH_BF
		LD HL,0X79ED			;OUT (C),A
		LD (0XFFFE),HL			;  OUT (C),A
		LD BC,0XBC77
		LD A,2
		JP 0XFFFE

;  64 
;: A= 16      
;      E=  64 
FLASH_64KB	LD HL,0
		PUSH AF
		LD A,E
		ADD A,2
		CALL SETWIN_INDICAT		;  
		POP AF
FLASHER2	PUSH AF
		LD BC,WIN_P0
		OUT (C),A			;  RAM     
		PUSH IX
		LD IX,0
FLASHER1	LD A,(IX)			;   
		INC A
		JR Z,FLASHER5			;  = FF,   
		DEC A
		CALL PGM_BYTE			; 
FLASHER5	INC IX
		INC HL
		LD A,H
		AND 0X0F
		OR L
		CALL Z,INC_INDICAT		;    4
		LD A,IXH
		AND 0X40
		JR Z,FLASHER1			;      
		POP IX
		POP AF
		INC A				;      
		LD D,A				; 
		LD A,H				;   64  
		AND A
		LD A,D				;
		JR NZ,FLASHER2
		RET
