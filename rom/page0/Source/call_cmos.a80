
; : H- 
;	   L- 
READCMOS	DI
		PUSH BC
		LD BC,SYSREG_EFF7
		LD A,(SYSREG1)
		OR CMOS_ON
		OUT (C),A
		LD B,HIGH (SET_ADR_CMOS)
		OUT (C),H
		LD B,HIGH (RD_WR_CMOS)
		IN L,(C)
		JR OFF_CMOS

; : H- 
;	   L-  
WRITECMOS	DI
		PUSH BC
		LD BC,SYSREG_EFF7
		LD A,(SYSREG1)
		OR CMOS_ON
		OUT (C),A
		LD B,HIGH (SET_ADR_CMOS)
		OUT (C),H
		LD B,HIGH (RD_WR_CMOS)
		OUT (C),L
OFF_CMOS	LD A,(SYSREG1)
		LD B,HIGH (SYSREG_EFF7)
		OUT (C),A
		POP BC
		LD A,L
		AND A
		EI
		RET

;  FPGA  BOOTLOADER,   
GET_VERS_EVO	LD H,0X11
		CALL READCMOS		;   
		LD H,0XF0
		CP 0XAA			;  ?
		LD L,0			;       FPGA
		JR Z,GVE6
		LD L,1			;      AVR
GVE6		LD C,L
		CALL WRITECMOS		;   0XF0   
		CALL READCMOS		; 
		CP C			;    ,  
		RET Z			;    (  FPGA ). 
		INC A			; 0XFF,   
		RET Z			;
		DEC A			;  
		EX AF,AF'
		LD A,6
		LD (COL_SYM),A
		LD DE,0X5B00
		EX AF,AF'
		LD (DE),A		;     
		INC E
		LD B,0X0F		;    15 
GVE1		INC H
		CALL READCMOS		; 
		LD (DE),A
		INC E
		DJNZ GVE1		;     
		LD E,0			;   12  
		LD B,0X0C		;     0 (  )
GVE2		LD A,(DE)
		AND A
		JR Z,GVE3
		INC E
		DJNZ GVE2
GVE3		LD BC,(0X5B0C)		;   2    
		LD A," "
		LD (DE),A		;   -
		INC E
		EX DE,HL

; 
UNVERS		LD A,C
		AND 0X1F		; 5 - 
		CALL A2TXT		;   
		SRL B
		RR C			;    
		LD A,C			;  
		RRCA
		RRCA
		RRCA
		RRCA			;   
		AND 0X0F		;   4  
		LD (HL),"."		; 
		INC HL
		CALL A2TXT		;   
		LD (HL),"."		; 
		INC HL
		LD (HL),"2"
		INC HL
		LD (HL),"0"		;    
		INC HL
		LD A,B			;  
		AND 0X3F		;  6 
		CALL A2TXT		;   
		BIT 6,B			;  6 (  7) 
		JR NZ,GVE4		;  
		EX DE,HL		;  ,      
		LD HL,TXT_BETA
		LD BC,5
		LDIR
		EX DE,HL
GVE4		LD (HL),0XFF
		LD DE,0X50E0
		LD L,0
		LD C,L
GVE5		LD A,(HL)
		INC L
		INC C
		INC A
		JR NZ,GVE5
		LD L,0
		LD A,0X20
		SUB C
		SRL A
		ADD A,E
		LD E,A
		JP PRTXT

; "A"     
A2TXT		PUSH HL
		LD L,A
		LD H,0
		LD DE,10
		XOR A
		DEC A
		INC A
		SBC HL,DE
		JR NC,$-3
		ADD HL,DE
		ADD A,"0"
		LD D,A
		LD A,L
		ADD A,"0"
		POP HL
		LD (HL),D
		INC HL
		LD (HL),A
		INC HL
		RET

;      0X11  0XAA  0X55,      
;   .     .
SET_CMOS_DEFAULT
		LD DE,CMOS_DEFAULT
		LD H,0X0A
		LD B,8
SCD1		EX DE,HL
		LD E,(HL)
		INC HL
		EX DE,HL
		CALL WRITECMOS
		INC H
		DJNZ SCD1
		RET

RDCMBCD PUSH HL
	LD H,0X0A
	CALL READCMOS
	RL L
	POP HL
	JR C,_af5e
	CALL READCMOS
	LD H,A
	LD A,0		;0=BCD
BCDon	EQU $-1
	AND A
	JR Z,_af5b	;dec to BCD
	LD A,H
	LD C,0XFE
	INC C
	INC C
	SUB 0X0A
	JR NC,$-4
	ADD A,0X0A
	RLC C
	RLC C
	RLC C
	OR C
	RET

_af5b	LD A,H
	AND A
	RET

_af5e	CALL OFF_CMOS
	SCF
	RET

;   ,    
PRINTTIME	LD A,(FLAGS)
		AND 4
		RET Z			;  ,    
		LD H,0X0C
		CALL READCMOS
		AND 0X10
		RET Z			;    ,    
		LD H,0X11
		CALL READCMOS
		CP 0X55
		RET Z			; ,    
		DI
		EXX
		CALL PRTT1		;   
		EXX
		EI
		RET
	
PRTT1	LD H,0X0B
	CALL RDCMBCD
	LD DE,0X5070
	BIT 5,A		;ࠧ襭 -  㤨쭨
	LD A,0X0C	;G
	CALL NZ,CMOSSYM
	LD HL,0X5A8C
	LD E,L
	LD BC,0X0806
	LD (HL),C
	INC L
	DJNZ $-2
	LD L,0XAC
	LD B,8
	LD (HL),C
	INC L
	DJNZ $-2
	LD H,4
	CALL TIM99
	CALL TIMMIG
	LD H,2
	CALL TIM99
	CALL TIMMIG
	LD H,0
	CALL TIM99
	LD H,6
	CALL RDCMBCD
	LD HL,0X4444
	LD (0X5ACF),HL
	LD E,0XCF
	JR C,PRTT01
	LD HL,PnWtSr1	;for BCD
adrPnWt	EQU $-2
        ADD A,A
	ADD A,L
	LD L,A
	JR NC,$+3
	INC H
	LD A,(HL)
	INC HL
	PUSH AF
	LD A,(HL)
	CALL CMOSSYM	;1
	POP AF
	CALL CMOSSYM	;2
PRTT01	LD E,0XAC
	LD H,7		;DD
	CALL TIM99
	LD A,0X0D	;.
	CALL CMOSSYM
	LD H,8		;MM
	CALL TIM99
	LD A,0X0D	;.
	CALL CMOSSYM
	LD H,9		;YY
TIM99	CALL RDCMBCD
	JR CMOSBCD

TIMMIG	LD H,0
	CALL RDCMBCD
	RRCA
	LD A,0X0A	;:
	JR C,CMOSSYM
	INC A		;spc
	JR CMOSSYM

iide	INC DE
	INC DE
	RET

CMOSBCD	JR C,iide
	PUSH AF
	RRCA
	RRCA
	RRCA
	RRCA
	AND 0X0F
	CALL CMOSSYM
	POP AF
	AND 0X0F
CMOSSYM	PUSH DE
	LD DE,CMOSFNT
	ADD A,A
	LD L,A
	ADD A,A
	ADD A,L
	LD L,A
	XOR A
	LD H,A
	ADD HL,DE
	POP DE
	LD B,6
	LD C,D
	LD (DE),A
	INC D
	LD A,(HL)
	LD (DE),A
	INC HL
	INC D
	DJNZ $-4
	XOR A
	LD (DE),A
	LD D,C
	INC E
	RET
	
CMOSFNT	DB 0X7F,0X63,0X63,0X63,0X63,0X7F
	DB 0X18,0X18,0X18,0X18,0X18,0X18
	DB 0X7F,0X03,0X7F,0X60,0X60,0X7F
	DB 0X7F,0X03,0X0F,0X03,0X03,0X7F
	DB 0X63,0X63,0X63,0X7F,0X03,0X03
	DB 0X7F,0X60,0X7F,0X03,0X03,0X7F
	DB 0X7F,0X60,0X7F,0X63,0X63,0X7F
	DB 0X7E,0X06,0X06,0X06,0X06,0X06
	DB 0X7F,0X63,0X7F,0X63,0X63,0X7F
	DB 0X7F,0X63,0X63,0X7F,0X03,0X7F
	DB 0X1C,0X1C,0X00,0X00,0X1C,0X1C	;:;A
	DB 0X00,0X00,0X00,0X00,0X00,0X00	;B
	DB 0X7E,0X81,0X9F,0X91,0X81,0X7E	;G;C
	DB 0X00,0X00,0X00,0X1C,0X1C,0X1C	;.;D
	DB 0X00,0X00,0X7E,0X42,0X42,0X42	;;E
	DB 0X00,0X00,0X42,0X7E,0X42,0X42	;;F
	DB 0X00,0X00,0X70,0X7E,0X42,0X7E	;;10
	DB 0X00,0X00,0X3E,0X08,0X08,0X08	;;11
	DB 0X00,0X00,0X7E,0X40,0X40,0X7E	;;12
	DB 0X00,0X00,0X7E,0X42,0X7E,0X40	;;13
	DB 0X00,0X3C,0X40,0X7C,0X42,0X7C	;;14
	DB 0X00,0X00,0X42,0X42,0X7E,0X02	;;15

	DW 0X1012	;      ..
PnWtSr0	EQU $-4		;for not BCD
PnWtSr1	EQU $-2		;for BCD
	DW 0X0E0F
	DW 0X1011
	DW 0X1213
	DW 0X1511
	DW 0X0E11
	DW 0X1214
	DW 0X1012
