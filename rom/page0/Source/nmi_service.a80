
;LAST UPDATE: 08.10.2012 savelij

;  RST 8

; 	HL AF BC RF ADR_RET DE	->
;->  	HL(    BF) DE(RAMNROM  DOS7FFD) BC' DE' HL' IX IY AF' ->
;-:  

RAM_SEL
		PHASE ADR_SEL_ROM
		OUT (C),A			;   
		NOP
FOR_RET		NOP
		LD (RST8_SAVE_SP),SP		;  
		LD SP,RREG_E-CPU2
		PUSH HL				;       BF
		PUSH DE				; RAMNROM+DOS7FFD
		EXX
		PUSH BC				;BC'
		PUSH DE				;DE'
		PUSH HL				;HL'
		EXX
		PUSH IX				;IX
		PUSH IY				;IY
		EX AF,AF'
		PUSH AF				;AF'
		LD (DNO_SAVE_SP),SP
		JR RAMSEL1

OUT_NMI		OUT (0XBE),A
		RETN

		DUPL 0X0038-$,0
IM_EI_RET	EI
		RET

RAMSEL1		LD A,0XC9
		LD (FOR_RET),A
		LD HL,(RST8_SAVE_SP)
		LD DE,RREG_E-CPU2
		LD BC,RREG_H-RPAGE_CALL
		LDIR
		LD HL,R_77-CPU2
		LD BC,RD_77
		INIR
		INI
		LD HL,(RDOS7FFD-CPU2)
		LD (R_DOS7FFD-CPU2),HL
		LD A,(R_77-CPU2)
		AND 7
		OR 0XA8
		LD BC,0XFF77
		JR RAMSEL2

		DUPL 0X0066-$,0XFF
		NOP
		JP NMI_SERVICE
ADR_NMI_JUMP	EQU $-2

		DUPL ADR_EXITRST8-$,0XFF
		JP EXIT_RST8

		DUPL ADR_EXITMAGIC-$,0XFF
		JP EXITNMISERVICE

RAMSEL2		OUT (C),A
		LD A,(R_7FFD-CPU2)
		AND 0X10
		LD HL,R_0WINA0-CPU2
		JR Z,RAMSEL3
		LD HL,R_1WINA0-CPU2
RAMSEL3		LD A,(RPAGE_CALL-CPU2)
		LD (HL),A
		LD BC,WIN_P2
		XOR A
		OUT (C),A
		LD B,HIGH (WIN_A0)
		LD A,5
		LD HL,CPU2
		ADD HL,SP
		LD SP,HL
		LD HL,CONT_RST8			;   RST 8
		PUSH HL
		JP ADR_SEL_ROM

		DEPHASE
ERAM_SEL

		PHASE ADR_LASTPAGE
EXIT_RST8	XOR A
		LD (FOR_RET),A
		LD C,LOW (WIN_A1)
		LD HL,B1_CPU1
		LD A,3
RENS12		EX AF,AF'
		LD A,(HL)
		INC HL
		LD B,(HL)
		INC HL
		OUT (C),A
		EX AF,AF'
		DEC A
		JR NZ,RENS12
		LD HL,RST8_SAVE_SP-(RREG_H-RPAGE_CALL)
		LD DE,(RST8_SAVE_SP)
		LD BC,RREG_H-RPAGE_CALL
		LDIR
		LD BC,0XBC77
		LD A,(R_77-CPU2)
		LD H,A
		AND 0X0F
		OR 0XA0
		LD E,A
		LD A,H
		AND 0X80
		RRCA
		OR B
		LD B,A
		LD A,H
		AND 0X60
		RLCA
		RLCA
		RLCA
		OR B
		LD B,A
		OUT (C),E
		LD SP,0
DNO_SAVE_SP	EQU $-2
		POP AF
		EX AF,AF'
		POP IY
		POP IX
		POP HL
		POP DE
		POP BC
		EXX
		POP DE
		POP HL
		LD SP,(RST8_SAVE_SP)		;   
		LD BC,ADR_RST8END		; 
		PUSH BC
		LD A,(R_7FFD-CPU2)
		AND 0X10
		LD BC,(B0_CPU0-CPU2)
		JR Z,RENS13
		LD BC,(B1_CPU0-CPU2)
RENS13		LD A,C				; ROM  
		LD C,LOW (WIN_A0)
		JP ADR_SEL_ROM

NMI_SERVICE	LD (NMI_SAVE_SP),SP		; 
		LD SP,NMI_SAVE_SP		; 
		PUSH HL				; HL
		PUSH AF				; AF
		LD A,I
		PUSH AF				; I,     
		LD A,R
		PUSH AF				; R
		PUSH DE				; DE
		PUSH BC				; BC
		EXX
		PUSH HL				;  HL
		PUSH DE				;  DE
		PUSH BC				;  BC
		PUSH IX				; IX
		PUSH IY				; IY
		EX AF,AF'
		PUSH AF				;  AF
		LD HL,(NMI_SAVE_SP)
		LD E,(HL)
		INC HL
		LD D,(HL)
		PUSH DE
		LD (RET_NMI_SP),SP		;     
		LD HL,OUT_NMI
		LD (ADR_NMI_JUMP),HL
		LD A,0XC9
		LD (FOR_RET),A			;RET    ROM/RAM
		LD HL,IM_EI_RET			;  INT
		LD (IM_WORK),HL
		LD HL,N_BF-CPU2			;    
		IN A,(PEVO_CONF)
		LD (HL),A
		INC HL
		LD BC,0X0CBE
NMISERV002	INIR
		INI
		LD BC,0XEFF7
		XOR A
		OUT (C),A
		PEC_ON SHADOW_BF
;		LD A,1
;		OUT (PEVO_CONF),A
		XOR A
		LD I,A				;     00FF
		LD BC,0XFF77
		LD A,(N_77-CPU2)
		AND 7
		OR 0XA0
		OUT (C),A
		LD DE,0XA70F			;   
		LD BC,0XBD77
		OUT (C),D			; 
		LD (ADR_PALITRA),HL
		LD BC,0XDBE
		EI
		HALT
		DI
NMISERV003	LD A,E
		BIT 3,E
		OUT (0XFE),A
		JR Z,NMISERV004
		OUT (0XF6),A
NMISERV004	IN A,(C)
		LD (HL),A
		INC HL
		DEC E
		JP P,NMISERV003			; 

		LD HL,MAGIC_PAL
		LD DE,0XA70F
		LD BC,0XBD77
		OUT (C),D			;.PAL
SETPAL1		LD A,E
		BIT 3,E
		OUT (0XFE),A
		JR Z,$+4
		OUT (0XF6),A
		LD A,(HL)
		INC HL
		OR %00001100
		OUT (0XFF),A
		DEC E
		JP P,SETPAL1

		LD BC,WIN_P2
		XOR A
		OUT (C),A			;   
		LD HL,CPU2
		ADD HL,SP
		LD SP,HL
		LD H,A
		LD L,A
		PUSH HL
		LD B,HIGH (WIN_A0)
		LD A,5
		OUT (C),A
		JP OUT_NMI

;  
EXITNMISERVICE	LD HL,ADR_MAGIC
		LD (HL),0XC9			;  MAGIC
		IN A,(PEVO_CONF)
		OR NMI_BF;SET 3,A
		OUT (PEVO_CONF),A
		AND NMI_BF!0XFF;RES 3,A
		OUT (PEVO_CONF),A
		HALT				;  MAGIC 
FALSE_NMI	LD HL,ADR_MAGIC
		LD (HL),0XC3			;  MAGIC  

		LD HL,0
ADR_PALITRA	EQU $-2
		LD A,(N_77-CPU2)
		LD E,0X0F
		AND E
		OR 0XA0
		LD D,A
		LD BC,0XBD77			;   
		OUT (C),D			;.PAL
SETPAL0		LD A,E
		BIT 3,E
		OUT (0XFE),A
		JR Z,$+4
		OUT (0XF6),A
		LD A,(HL)
		INC HL
		OR %00001100
		OUT (0XFF),A
		DEC E
		JP P,SETPAL0
		LD C,LOW (WIN_A0)
		LD A,(N_7FFD-CPU2)
		AND 0X10
		LD HL,B0_CPU0-CPU2
		JR Z,ENS11
		LD HL,B1_CPU0-CPU2
ENS11		LD A,4
ENS12		EX AF,AF'
		LD A,(HL)
		INC HL
		LD B,(HL)
		INC HL
		OUT (C),A
		EX AF,AF'
		DEC A
		JR NZ,ENS12
		LD HL,(N_BF-CPU2)
		LD BC,0XBC77
		LD A,H
		AND 0X0F
		OR 0XA0
		LD E,A
		LD A,H
		AND 0X80
		RRCA
		OR B
		LD B,A
		LD A,H
		AND 0X60
		RLCA
		RLCA
		RLCA
		OR B
		LD B,A
		OUT (C),E
		LD A,L
		OUT (PEVO_CONF),A
		LD BC,0X7FFD
		LD A,(N_7FFD-CPU2)
		OUT (C),A
		LD BC,PENT_CONF
		LD A,(N_EFF7-CPU2)
		OUT (C),A
		LD A,0XED
		LD (FOR_RET),A
		LD SP,0
RET_NMI_SP	EQU $-2
		LD HL,NMI_SERVICE
		LD (ADR_NMI_JUMP),HL
		POP AF				;  
		POP AF
		EX AF,AF'
		POP IY
		POP IX
		POP BC
		POP DE
		POP HL
		EXX
		POP BC
		POP DE
		EI
		HALT
		POP AF
		LD R,A
		JP PO,ENMISERV1
		EI
ENMISERV1	POP AF
		LD I,A
		JP PO,ENMISERV0
		EI
ENMISERV0	POP AF
		POP HL
		LD SP,(NMI_SAVE_SP)
		OUT (0XBE),A
		RETN

MAGIC_PAL	DB 0X0C,0X2D,0X4E,0X6F,0X9C,0XBD,0XDE,0XFF
		DB 0XEC,0XED,0XEE,0XEF,0XFC,0XFD,0XFE,0XFF

		DEPHASE
ENMI_SERVICE
