
; : A-  
		DI
		AND A
		LD HL,READ_SD
		JR Z,1F
		DEC A
		LD HL,READNGS
		JR Z,1F
		DEC A
		LD HL,RD_HDDN
		JR Z,1F
		LD HL,RD_HDDS	
1		LD (LOADSCS1),HL	;  
		EXA
		AND A
		LD A,#E0
		JR Z,5F
		LD A,#F0
5		LD (MASSLAN),A
		LD (MASSLAS),A
		CALL REALSEC		;   
		CALL LOADLST		;  
		EXX
		LD HL,(BUF_512+9)
		LD (ASTART),HL		;  
		LD HL,(BUF_512+#0B)
		LD (LENGHT),HL		;    
		LD DE,#11
		ADD HL,DE
		LD A,L
		AND A
		JR Z,2F			;    0
		INC H			; +1
2		LD A,H
		SRL A			;  2     512
		ADC A,0			; /
		DEC A			;  ,   1 
		EXX
		JP Z,LDIRBUF		;  1   
		LD LX,A			;  -1
		EXX
		CALL LD_ONES		;   
		PUSH DE			;   
		EXX
		LD HL,1			;   
		ADD HL,DE		;     
		EX DE,HL
		JR NC,4F
		INC BC			;BCDE=BCDE+1
4		POP HL			; HL    
		LD IY,(BYTSSEC)		;LY=-   
		LD A,LX
		CP LY			;     
		JR C,2F			;    
		INC LX
		LD A,LY
		CP 2
		JR C,1F
		DEC A
		JR 5F

1		PUSH HL
		CALL RDFATZP
		CALL LST_CLS
		POP HL
		JR C,RUNLOAD
		PUSH HL
		CALL REALSEC
		POP HL
		LD A,LX
		CP LY
		JR C,2F
		LD A,LY
5		CALL LOADSCS
		LD A,LX
		SUB LY
		JR Z,RUNLOAD
		LD LX,A
		JR 1B

2		AND A
		JR Z,3F
		CALL LOADSCS
3		PUSH HL
		LD A,LX
		DEC A
		LD L,A
		LD H,0
		ADD HL,DE
		EX DE,HL
		JR NC,4F
		INC BC
4		CALL LOADLST
		LD BC,0
LENGHT		EQU $-2
		EX DE,HL
		LD HL,#11
		ADD HL,BC
		LD B,H
		LD C,L
		EX DE,HL
		POP DE
		LD A,B
		AND 1
		LD B,A
		LDIR
; 
RUNLOAD		LD HL,#2758		
		EXX
		LD IY,#5C3A
		EI
		JP 0
ASTART		EQU $-2

LDIRBUF		LD HL,#200-#11
		LD DE,(BUF_512+#0B)
		AND A
		SBC HL,DE
		JR NC,1F
		CALL LD_ONES
		JP RUNLOAD

1		LD HL,BUF_512+#11
		LD DE,(BUF_512+9)
		LD BC,(BUF_512+#0B)
		LDIR
		JP RUNLOAD

;     ,  =501    
LD_ONES		LD HL,BUF_512+#11
		LD DE,(BUF_512+9)
		LD BC,#200-#11
		LDIR
		RET

;    
LOADSCS		JP 0
LOADSCS1	EQU $-2

LST_CLS		LD A,(CAL_FAT)
		AND A
		JR NZ,LST_CL1
		LD HL,#0FF7
		SBC HL,DE
		RET

LST_CL1		DEC A
		JR NZ,LST_CL2
		LD HL,#FFF7
		SBC HL,DE
		RET

LST_CL2		LD HL,#0FFF
		SBC HL,BC
		RET NZ
		LD HL,#FFF7
		SBC HL,DE
		RET

RDFATZP		LD BC,(AFILCLS+2)
		LD DE,(AFILCLS)
		LD A,(CAL_FAT)
		AND A
		JR Z,RDFATS0
		DEC A
		JR Z,RDFATS1
		EX DE,HL
		ADD HL,HL
		EX DE,HL
		LD HL,0
		ADC HL,BC
		ADD HL,BC
		LD A,E
		LD E,D
		LD D,L
		LD C,H
		LD B,0
		CALL RDFATS2
		INC HL
		LD C,(HL)
		INC HL
		LD B,(HL)
1		LD (AFILCLS+2),BC
		LD (AFILCLS),DE
		RET

RDFATS1		LD BC,0
		LD A,E
		LD E,D
		LD D,C
RDFATS2		PUSH AF
		PUSH BC
		LD HL,FATSTR
		CALL BCDEHLP
		CALL LOADLST
		POP BC
		POP AF
		LD E,A
		LD D,0
		ADD HL,DE
		ADD HL,DE
		LD E,(HL)
		INC HL
		LD D,(HL)
		JR 1B

RDFATS0		LD H,D
		LD L,E
		ADD HL,HL
		ADD HL,DE
		SRL H
		RR L
		LD A,E
		LD E,H
		LD D,0
		LD B,D
		LD C,D
		SRL E
		PUSH AF
		PUSH HL
		LD HL,FATSTR
		CALL BCDEHLP
		CALL LOADLST
		POP BC
		LD A,B
		AND 1
		LD B,A
		ADD HL,BC
		LD B,(HL)
		INC HL
		LD A,H
		CP HIGH BUF_512+2
		JR NZ,RDFATS4
		PUSH BC
		LD BC,0
		INC DE
		CALL LOADLST
		POP BC
RDFATS4		POP AF
		LD D,(HL)
		LD E,B
		LD BC,0
		RRA
		JR NC,RDFATS3
		DUP 4
		SRL D
		RR E
		EDUP
RDFATS3		LD A,D
		AND #0F
		LD D,A
		JR 1B

;  
;  BCDE= FAT
;  BCDE= 
REALSEC		LD BC,(AFILCLS+2)
		LD DE,(AFILCLS)
		LD A,B
		OR C
		OR D
		OR E
		JR NZ,REALSE1
		LD DE,(FATSTR)
		LD BC,(FATSTR+2)
		LD HL,SEC_FAT
		PUSH HL
		CALL BCDEHLP
		POP HL
		JP BCDEHLP

REALSE1		LD HL,#FFFE
		EX DE,HL
		ADD HL,DE
		EX DE,HL
		INC HL
		ADC HL,BC		; -2
		LD A,(BYTSSEC)
		JR 1F

2		SLA E
		RL D
		RL L
		RL H
1		RRCA
		JR NC,2B		;   
		LD B,H
		LD C,L
		LD HL,STARTRZ
		CALL BCDEHLP		;    
		LD HL,FRSTDAT
		JP BCDEHLP		;    

BCDE200		LD E,D
		LD D,C
		LD C,B
		LD B,0
		LD A,2
		JR BCDE_A

;BCDE>>A=BCDE
BCDE_A1		SRL B
		RR C
		RR D
		RR E
BCDE_A		RRCA
		JR NC,BCDE_A1
		RET

;(ADR)-BCDE=BCDE
BCDEHLM		LD A,(HL)
		INC HL
		SUB E
		LD E,A
		LD A,(HL)
		INC HL
		SBC A,D
		LD D,A
		LD A,(HL)
		INC HL
		SBC A,C
		LD C,A
		LD A,(HL)
		SBC A,B
		LD B,A
		RET

;(ADR)+BCDE=BCDE
BCDEHLP		LD A,(HL)
		INC HL
		ADD A,E
		LD E,A
		LD A,(HL)
		INC HL
		ADC A,D
		LD D,A
		LD A,(HL)
		INC HL
		ADC A,C
		LD C,A
		LD A,(HL)
		ADC A,B
		LD B,A
		RET

;HLDE+BC=HLDE
HLDEPBC		EX DE,HL
		ADD HL,BC
		EX DE,HL
		LD BC,0
		ADC HL,BC
		RET

;   
LOADLST		LD HL,BUF_512
		PUSH HL
		LD A,1
		CALL LOADSCS
		POP HL
		RET

;================  ZC SD ===================
READ_SD		PUSH AF
		LD A,1
		OUT (P_CONF),A
		POP AF
		EXA
		LD A,CMD_18
		CALL SECM200
		EXA
RDMULT1		EXA
		CALL IN_OOUT
		CP #FE
		JR NZ,$-5
		PUSH BC
		LD BC,P_DATA
		INIR
		NOP
		INIR
		NOP
		IN A,(C)
		NOP
		IN A,(C)
		POP BC
		EXA
		DEC A
		JR NZ,RDMULT1
		LD A,CMD_12
		CALL OUT_COM
		CALL IN_OOUT
		INC A
		JR NZ,$-4
		PUSH AF
		LD A,3
		OUT (P_CONF),A
		XOR A
		OUT (P_DATA),A
		POP AF
		RET

SECM200		PUSH HL
		PUSH DE
		PUSH BC
		PUSH AF
		PUSH BC
		LD A,CMD_58
		LD BC,P_DATA
		CALL OUT_COM
		CALL IN_OOUT
		IN A,(C)
		NOP
		IN H,(C)
		NOP
		IN H,(C)
		NOP
		IN H,(C)
		BIT 6,A
		POP HL
		JR NZ,SECN200
		EX DE,HL
		ADD HL,HL
		EX DE,HL
		ADC HL,HL
		LD H,L
		LD L,D
		LD D,E
		LD E,0
SECN200		POP AF
		LD BC,P_DATA
		OUT (C),A
		NOP
		OUT (C),H
		NOP
		OUT (C),L
		NOP
		OUT (C),D
		NOP
		OUT (C),E
		LD A,#FF
		OUT (C),A
		POP BC
		POP DE
		POP HL
		RET

OUT_COM		PUSH BC
		LD BC,P_DATA
		OUT (C),A
		XOR A
		OUT (C),A
		NOP
		OUT (C),A
		NOP
		OUT (C),A
		NOP
		OUT (C),A
		DEC A
		OUT (C),A
		POP BC
		RET

IN_OOUT		PUSH DE
		LD DE,#20FF
IN_WAIT		IN A,(P_DATA)
		CP E
		JR NZ,IN_EXIT
IN_NEXT		DEC D
		JR NZ,IN_WAIT
IN_EXIT		POP DE
		RET
;=========================================================

;===============  SD NEOGS=========================
; "A" 
READNGS		EXA
		LD A,3
		CALL COMM2SD
		EXA
		PUSH DE
		PUSH BC
		LD BC,GSDAT
SDRDSN1		EXA
		OUT (GSCOM),A
		CALL WC_
		LD DE,#0200
SDRDSN2		CALL WN_
		INI
		DEC DE
		LD A,D
		OR E
		JR NZ,SDRDSN2
		EXA
		DEC A
		JR NZ,SDRDSN1
		CALL WN_
		IN A,(C)
		CP #77
		JR NZ,$-4
		POP BC
		POP DE
		XOR A
		RET

; /    NeoGS
COMM2SD		OUT (GSDAT),A		;  
		LD A,#1E
		OUT (GSCOM),A		;  
		CALL WC_
		LD A,B
		OUT (GSDAT),A		;  31-24 
		CALL WD_
		LD A,C
		OUT (GSDAT),A		;  23-16 
		CALL WD_
		LD A,D
		OUT (GSDAT),A		;  15-8 
		CALL WD_
		LD A,E
		OUT (GSDAT),A		;  7-0 
		CALL WD_
		EXA
		OUT (GSDAT),A		; - 
		EXA
		RET

;  NeoGS  
WD_		IN A,(GSCOM)
		RLA
		JR C,$-3
		RET

;  NeoGS  
WN_		IN A,(GSCOM)
		RLA
		JR NC,$-3
		RET

;  NeoGS  
WC_		IN A,(GSCOM)
		RRA
		JR C,$-3
		RET
;==========================================================

;==================  NEMO HDD=======================
P_1F7		EQU #F0			; / 
P_1F6		EQU #D0			;CHS-   /LBA  24-27
P_1F5		EQU #B0			;CHS- 8-15/LBA  16-23
P_1F4		EQU #90			;CHS- 0-7/LBA  8-15
P_1F3		EQU #70			;CHS- /LBA  0-7
P_1F2		EQU #50			; 
P_1F1		EQU #30			; /
P_1F0		EQU #10			; 
P_3F6		EQU #C8			; /
P_HI		EQU #19			; 8 
PRT_RW		EQU P_1F0*256+P_HI	; /  

RD_HDDN		PUSH BC
		PUSH DE
		PUSH DE
		EXA
		LD A,B
		LD E,C
		LD BC,#FF00+P_1F6
		OR 0
MASSLAN		EQU $-1
		OUT (C),A
		LD C,P_1F5
		OUT (C),E
		POP DE
		LD C,P_1F4
		OUT (C),D
		LD C,P_1F3
		OUT (C),E
		LD C,P_1F2
		OUT (C),A
		LD C,P_1F7
		LD A,#20
		OUT (C),A
		LD C,P_1F7
HDDRD1		IN A,(C)
		AND #88
		CP 8
		JR NZ,HDDRD1
		EXA
HDDRD2		EXA
		LD A,#40
		LD C,P_1F0
READSC1		DUP 4
		IN E,(C)
		INC C
		IN D,(C)
		DEC C
		LD (HL),E
		INC HL
		LD (HL),D
		INC HL
		EDUP
		DEC A
		JR NZ,READSC1
		LD C,P_1F7
HDDRD3		IN A,(C)
		AND #80
		JR NZ,HDDRD3
		EXA
		DEC A
		JR NZ,HDDRD2
		POP DE
		POP BC
		XOR A
		RET
;=============================================================

;==================   =======================
PS1F7		EQU #FF		;#FFBE	 / 
PS1F6		EQU #FE		;#FEBE	CHS-   /LBA  24-27
PS1F5		EQU #FD		;#FDBE	CHS- 8-15/LBA  16-23
PS1F4		EQU #FC		;#FCBE	CHS- 0-7/LBA  8-15
PS1F3		EQU #FB		;#FBBE	CHS- /LBA  0-7
PS1F2		EQU #FA		;#FABE	 
PS1F1		EQU #F9		;#F9BE	 /
PS1F0		EQU #F8		;#F8BE	 
PS3F6		EQU #FE		;#FEBE	 /
PSHI		EQU #D8		;#D8BE	 8 
PRTSRW		EQU PS1F0*256+PSHI	; /  
LOW_PRT		EQU #BE			;   SMUC IDE
SMUCSYS		EQU #FFBA		;  SMUC
SMUCVER		EQU #5FBA		;  SMUC

RD_HDDS		PUSH BC
		PUSH DE
		CALL SETSREG
		EXA
		LD B,PS1F7
		LD A,#20
		CALL SOUTPRT
HDSRDM2		CALL SINPRT
		AND #88
		CP 8
		JR NZ,HDSRDM2
		EXA
HDSRDM1		PUSH AF
		LD DE,PRTSRW
		LD A,#40
RDCSSC1		EXA
		DUP 4
		LD B,D
		CALL SINPRT
		LD (HL),A
		INC HL
		LD B,E
		CALL SINPRT
		LD (HL),A
		INC HL
		EDUP
		EXA
		DEC A
		JR NZ,RDCSSC1
		LD B,PS1F7
HDSRDM3		CALL SINPRT
		AND #80
		JR NZ,HDSRDM3
		POP AF
		DEC A
		JR NZ,HDSRDM1
		POP DE
		POP BC
		RET
	
SETSREG		PUSH DE
		LD D,B
		LD E,C
		EXA
		LD BC,(PS1F6*#0100)+LOW_PRT
		LD A,D
		OR 0
MASSLAS		EQU $-1
		CALL SOUTPRT
		LD B,PS1F5
		LD A,E
		CALL SOUTPRT
		POP DE
		LD B,PS1F4
		LD A,D
		CALL SOUTPRT
		LD B,PS1F3
		LD A,E
		CALL SOUTPRT
		LD B,PS1F2
		EXA

SOUTPRT		PUSH HL
		LD HL,#3FF0
		EX (SP),HL
		JP #3D2F

SINPRT		PUSH HL
		LD HL,#3FF3
		EX (SP),HL
		JP #3D2F

;=============================================================