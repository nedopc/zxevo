
;LAST UPDATE: 12.11.2014 savelij

		include ../../macros.a80
		include ../../define.a80
		include ../../global_vars.a80

DEPKADR		EQU 0X6000

		ORG 0X0000
		DI
		JP INITPAGE0

		DUPL 0X0008-$,0XFF
		NOP
		JP DOS2RST8

		DUPL ADR_SEL_ROM-$,0XFF
		OUT (C),A
		NOP
		RET

		DUPL 0X0038-$,0XFF
		EI
		RET

		DUPL 0X0040-$,0XFF
		JP SET_MAPPER

		DUPL 0X0066-$,0XFF
		NOP

INITPAGE0	LD SP,DEPKADR
		LD IY,0X5C3A
		LD A,0X3F
		LD I,A
		IM 1
		XOR A
		OUT (0XFE),A
		LD HL,PAGES_CONF
		LD BC,0XBC77
		LD A,2
		OUT (C),A
		LD DE,0X103F
INITPAGE2	LD BC,CONF_128
		OUT (C),D
		LD BC,LOW (WIN_A0)
INITPAGE1	OUTI
		LD A,B
		SUB E
		LD B,A
		JR NZ,INITPAGE1
		LD BC,CONF_128
		LD A,D
		XOR 0X10
		LD D,A
		JR Z,INITPAGE2
		LD BC,0XFF77
		LD A,0XAB
		OUT (C),A
		PEC_ON SHADOW_BF
		LD HL,INITPAGE3
		PUSH HL
		LD HL,INST_NMIRST
		PUSH HL
		LD BC,WIN_A0
		LD A,P_ADDON1
		JP ADR_SEL_ROM

INITPAGE3	RST8 _CMOS_RW,_INIT_CMOS
		LD HL,PAGES_CONF
		LD A,0XEF
		IN A,(0XFE)
		RRCA				;‚…€ €†€’  "0"  ‘‘…
		JR NC,RSTRAM			;‘‘ ‚ ‘‚… ‡“
		RRCA
		RRCA
		RRCA
		RRCA				;‚…€ €†€’  "6"  ‘‘…
		JR C,INITPAGE4
;‚•„ ‚ ’‹€„—
		EXX
		LD BC,WIN_P2
		XOR A
		OUT (C),A
		LD HL,CPU2+DEBUG_ONOFF
		SET 7,(HL)
		EXX
		IN A,(PEVO_CONF)
		OR NMI_BF
		OUT (PEVO_CONF),A
		AND NMI_BF!0XFF
		OUT (PEVO_CONF),A
		HALT
INITPAGE4	;	PEC_ON EMUVG_BF		;‚…€
		LD BC,CMOSD_SET_ADR
		LD A,CMOS_BYTE_00
		OUT (C),A
		LD B,HIGH (CMOSD_RD_WR)
		IN A,(C)
		AND MKOL_MODES
		CP KOL_MODES
		JR NC,RSTRAM
		AND A
		JR Z,RSTRAM
		DEC A
		LD HL,PAGES_CONFGLUK		;”ƒ“‚€’ „‹ GLUK
		JR Z,RSTRAM
		DEC A
		LD HL,PAGES_PROFROM		;”ƒ“‚€’ „‹ PROFROM
		JR Z,RSTRAM
		LD HL,PAGES_CUSTOM		;”ƒ“‚€’ „‹ USER ROM
RSTRAM		LD A,0XEF
		IN A,(0XFE)
		RRCA				;‚…€ … €†€’‘’  "0"  ‘‘…
		JR NC,RSTRAM			;…‘‹ €†€’€ †„… ’“‘€
		LD BC,0XBC77
		LD A,2
		OUT (C),A
		LD DE,0X103F
LOOP2		LD BC,CONF_128
		OUT (C),D
		LD BC,LOW (WIN_A0)
LOOP1		OUTI
		LD A,B
		SUB E
		LD B,A
		JR NZ,LOOP1
		LD BC,CONF_128
		LD A,D
		XOR 0X10
		LD D,A
		JR Z,LOOP2
RSTPAL		LD A,(HL)
		LD IXH,A			;… ‘’€–› ‡€‚…… ’€ €€ƒ…€ 
		LD DE,0XAB0F			;ZX SCREEN MODE, TURBO ON 14MHZ
		LD BC,0XFD77			; „‘’“  €‹’…
		OUT (C),D
		EI				;― «¨βΰ 
		HALT
		DI
		LD HL,TRSTPAL			;+15
		LD BC,0XBD77
		OUT (C),D			;Ά«.PAL
RSTPAL0		LD A,E
		BIT 3,E
		OUT (0XFE),A
		JR Z,$+4
		OUT (0XF6),A
		LD A,(HL)
		INC HL
		OR %00001100
		OUT (0XFF),A
		DEC E
		JP P,RSTPAL0
END_INIT	LD A,0XAB
		LD BC,0XFF77
		OUT (C),A			;Άλ«.PAL
		LD A,IXH
		LD BC,0
		AND 0X3C
		JR Z,RSTKEYS2			;RESET TO EVO SERVICE PAGE
		CP CONF4GLUK&0X3F
		JR Z,RSTKEYS3			;RESET TO GLUK SERVICE PAGE
		CP CONF4CUSTOM&0X3F
		JR Z,RSTKEYS3			;RESET TO CUSTOM ROM
		JP LDIR4PROFROM			;RESET TO PROFROM BASIC 128

RSTKEYS3	PUSH BC
		LD BC,0XBC77
		LD A,2
		OUT (C),A
		LD BC,WIN_A0
		LD A,IXH
		OUT (C),A
		PEC_OFF SHADOW_BF
		LD BC,0XFF77
		LD A,0XA3
		JP 0X3D2D

RSTKEYS2	LD H,CMOS_BYTE_00
		RST8 _CMOS_RW,_READ_CMOS
		AND 0XFC			;‘‘ „‹ ‘‚ƒ ‡“
		LD L,A
		RST8 _CMOS_RW,_WRITE_CMOS			;“„’…‹ ‚›‘’€‚‹…… ‚ ‘ ‘‘€ € EVO SERVICE
		LD BC,WIN_P2
		LD A,PAGE_EVODOS
		OUT (C),A
		LD B,HIGH (WIN_A3)
		LD A,P_ADD_DOS
		OUT (C),A			
		LD HL,CPU3
		LD DE,CPU2
		LD BC,0X4000
		LDIR				;……‘ DOS
		LD BC,WIN_A2
		LD A,0X7D
		OUT (C),A
		LD B,HIGH (WIN_A3)
		LD A,0XFF
		OUT (C),A
		LD BC,0XFEFF
RSTKEYS1	LD A,B
		IN A,(0XFE)
		REPT 5
		RRA
		JR C,$+3
		INC C
		ENDM
		RLC B
		JR C,RSTKEYS1
		DEC C
		JP P,GTSTKEY			;€†€’ ‹…… 1 , ……•„  ’…‘’“ ‹€‚€’“›
		LD IXL,0			;‡€“‘ …‘‚‰ ‚ ‘…‚‘€
		LD DE,0				;1 €†€’€, ‚…… —’ ’
		LD BC,CONF_128
		PUSH DE				;€„…‘ ……•„€ ‚ ‡“ …‘‹ ‚›€
		LD A,0XFE
		IN A,(0XFE)			;—’…… €†€’‘’  "CS"
		RRA
		LD A,0
		JP NC,START_SELECT		; €†€’‰ "CS" ……•„ ‚ …‰‘ 128
		LD A,0X7F
		IN A,(0XFE)			;—’…… €†€’‘’  "SS"
		LD D,A
		RRA
		RRA
		LD A,0X30
		JP NC,START_SELECT		; €†€’‰ "SS" ……•„ ‚ …‰‘ 48
		LD A,D
		RRA				;‚…€ €†€’‘’  "SPACE"
		JR C,CMPCFG1			;……•„ „€‹…… …‘‹ —…ƒ … €†€’
		LD A,0X10
		LD DE,0X3D2F
		PUSH DE
		JP START_SELECT			; €†€’‰ "SPACE" ……•„ ‚ TR-DOS

CMPCFG1		LD A,0XFD
		IN A,(0XFE)
		AND 4				;€†€’ "D" ‡€“‘ „…
		JP Z,GDEMO
		LD IXL,1			;‡€“‘ ‘‚‰ ‚ ‘…‚‘€
		LD HL,DEPKADR			;—…ƒ … €†€’, ‡€“‘€… ‘‚“ ‚“
		LD SP,HL
		EX DE,HL
		LD HL,LOADADR			;€‘€‚€ … ƒ‹€
		CALL DEC40
		XOR A
		IN A,(0XFE)
		CPL
		AND 0X1F
		LD A,0X10
		JR NZ,CMOSHELP
		LD HL,0X6000
		PUSH HL
		JR START_SELECT

GTSTKEY		SCF
		LD A,0X10
CMOSHELP	LD HL,BONUADR			;CMOS setup & HELP
		LD DE,0X6000			;€‘€‚€  ‡€“‘ CMOS SETUP
		PUSH DE
		PUSH AF
		CALL DEC40
		POP AF
		JR START_SELECT

GDEMO		LD HL,0X6000-szdemoini
		LD SP,HL
		EX DE,HL
		PUSH DE
		LD HL,DEMO
		LD B,3
		LDIR
START_SELECT	EX AF,AF'			;‘•€…… €  ”‹€ƒ‚
		LD HL,ERAM_CODE-1
		LD DE,0X5C80
		LD BC,ERAM_CODE-RAM_CODE
		LDDR
		EX DE,HL
		INC HL
		EXX
		LD A,IXL
		AND A
		LD D,0XA3
		JR NZ,SET_MODE3
		LD H,0XEF
		CALL READCMOS
		AND 0X80
		REPT 4
		RRCA
		ENDM
		OR 0XA3
		LD D,A				;“‘’€‚€ „‹ 14ƒ–
SET_MODE3	LD H,0X0E
		CALL READCMOS
		LD E,A				;“‘’€‚€ „‹ 7.0/3.5ƒ–  „…‹ €’
		PEC_OFF SHADOW_BF
		EXX
CMPCFG2		JP (HL)

RAM_CODE	EXX
		LD BC,PENT_CONF
		LD A,IXL
		AND A
		LD A,0X10
		JR NZ,SET_MODE2
		LD A,E
		AND 3
		LD L,0
		JR Z,SET_MODE1
		LD L,4
SET_MODE1	LD A,E
		AND 0X80
		REPT 3
		RRCA
		ENDM
		OR L
		BIT 3,D
		JR Z,SET_MODE2
		SET 4,A
SET_MODE2	OUT (C),A
		LD BC,CONF_128
		XOR A
		OUT (C),A			;‚‹—‹ €… 0
		PEC_ON SHADOW_BF
		LD BC,WIN_A0
		LD A,IXH
		OUT (C),A			;‡€ƒ€‚€‹ ROM „‹ €… 0
		LD BC,0XFF77
		OUT (C),D			;‚›‘’€‚‹ ’“ ‚ ‡€‚‘‘’ ’ “‘’€‚
		PEC_OFF SHADOW_BF
		EX AF,AF'
		LD BC,CONF_128
		OUT (C),A			;‚‹—‹ €… 1
		RET
ERAM_CODE

;€ ‚•„…:  H-€„…‘ —…‰
;€ ‚›•„…: L,A-—’€… ‡€—……
READCMOS	PUSH BC
		LD BC,CMOSD_SET_ADR
		OUT (C),H
		LD B,HIGH (CMOSD_RD_WR)
		IN L,(C)
		LD A,L
		POP BC
		RET

BONUADR		binclude cmosset_pack.rom
LOADADR		binclude main_pack.rom

;”ƒ“€– „‹ ’„…‹ ‚€…‰ 64 ‚ (USER ROM)
PAGES_CUSTOM	DB 0XFF,0X7D,0X7A,CONF4CUSTOM+3
		DB 0XFF,0X7D,0X7A,0X00,CONF4CUSTOM+1

		DUPL ADR_RST8END-$,0XFF
EDOS2RST8	LD A,L
		OUT (PEVO_CONF),A
		POP DE
		POP HL
		POP AF
		JP PO,EDOS2RST01
		EI
EDOS2RST01	POP BC
		POP AF
		EX (SP),HL
		RET

DOS2RST8	EX (SP),HL			;HL
		PUSH AF				;AF
		PUSH BC				;BC
		LD A,R
		JP PE,DOS2RST8_01
		LD A,R
DOS2RST8_01	DI
		PUSH AF				;RF
		PUSH HL				;ADR_RET
		PUSH DE				;DE
		IN A,(PEVO_CONF)
		LD L,A
		OR SHADOW_BF
		OUT (PEVO_CONF),A
		LD BC,RD_0WINA0
		IN H,(C)
		LD B,HIGH (RD_DOS7FFD)
		IN E,(C)
		RES 4,E
		DEC B
		IN D,(C)
		LD BC,WIN_P0
		XOR A
		JP ADR_SEL_ROM

	       	DUPL 0X3D2D-$,0XFF
		OUT (C),A
		NOP
		RET

DEMO		LD BC,CONF_128
		LD A,0X10
		OUT (C),A
		EI
szdemoini	EQU $-DEMO

		binclude grass.bin

		include ../../dec40.a80

LDIR4PROFROM	LD HL,JMP2PROFROM
		LD DE,JMP2PROFROM+0X8000
		LD BC,EJMP2PROFROM-JMP2PROFROM
		LDIR
		PUSH BC
		LD BC,0XBC77
		LD A,3
		OUT (C),A
		LD A,IXH
		LD BC,WIN_A0
		OUT (C),A
		LD BC,0XFF77
		LD A,0XA3
		JP JMP2PROFROM+0X8000

JMP2PROFROM	OUT (C),A
		RET
EJMP2PROFROM	

SET_MAPPER	
		RET

;1000 0001 81 0 BASIC 128	|EVO
;1000 0011 83 0 BASIC 48	|SERVICE

;1000 0101 85 1 BASIC 128	|ADDONS
;1000 0111 87 1 BASIC 48 	|RST8 SERVICE

;1000 1001 89 2 BASIC 128	|RESERVED
;1000 1110 8B 2 BASIC 48	|EVODOS EMUL 3D2F
                                          
;1000 1101 8D 3 BASIC 128	|PROFROM 1
;1000 1111 8F 3 BASIC 48 	|PROFROM 1
                                          
;1001 0001 91 4 BASIC 128	|PROFROM 0
;1001 0011 93 4 BASIC 48 	|PROFROM 0

;1001 0101 95 5 BASIC 128	|GLUK
;1001 0111 97 5 BASIC 48 	|GLUK

;1001 1001 99 6 BASIC 128	|FREE
;1001 1011 9B 6 BASIC 48	|FREE

;1001 1101 9D 7 BASIC 128	|CUSTOM
;1001 1111 9F 7 BASIC 48	|CUSTOM

;‘‚€ ”ƒ“€–  ‘‘“, ‡…’ €’…ƒ—…‘ … ……„“…’‘
PAGES_CONF	DB 0XFF,0X7D,0X7A,0X83
		DB 0XFF,0X7D,0X7A,0X00,0X81

;”ƒ“€– „‹ GLUK
PAGES_CONFGLUK	DB 0XFF,0X7D,0X7A,CONF4GLUK+3
		DB 0XFF,0X7D,0X7A,0X00,CONF4GLUK+1

;”ƒ“€– „‹ EVO PROFROM
PAGES_PROFROM	DB 0XFF,0X7D,0X7A,CONF4PROF+3
		DB 0XFF,0X7D,0X7A,0X00,CONF4PROF+1

;€‹’€ „‹ SPECTRUM …†€
TRSTPAL		DB 0X00,0X21,0X42,0X63,0X90,0XB1,0XD2,0XF3
		DB 0XE0,0XE1,0XE2,0XE3,0XF0,0XF1,0XF2,0XF3

		DUPL 0X3FF8-$,0XFF
		DB "HEGLUK"
		DW DATA_VERS
