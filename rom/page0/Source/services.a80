
DD		EQU 06				;„€’€
MM		EQU 10				;Œ…‘Ÿ–
YY		EQU 10				;ƒŽ„
DATA		EQU DD+(MM<<5)+(YY<<9);+0X8000	;“†… “€ŠŽ‚€Ž

DEPKADR		EQU 0X6000

SYSREG_EFF7	EQU 0XEFF7
SET_ADR_CMOS	EQU 0XDFF7
RD_WR_CMOS	EQU 0XBFF7
CMOS_ON		EQU 0X80
CMOS_OFF	EQU 0

		ORG 0X0000

		DI
		LD IY,0X5C3A
		LD A,0X3F
		LD I,A
		IM 1
		XOR A
		OUT (0XFE),A
		LD IXL,"P"		;…‘‹ˆ … Ž…„…‹ˆ’‘Ÿ, ’Ž “„…’ Ž˜ˆ‚Š€ „‹Ÿ …’€ƒŽ€
		LD HL,4
		LD D,0X40
		LD E,L
		LD B,0X80
		LD C,L
CMPROM1		LD A,(BC)
		CP (HL)
		JP NZ,RSTKEYS
		EX DE,HL
		CP (HL)
		EX DE,HL
		JP NZ,RSTKEYS
		DEC L
		DEC E
		DEC C
		JR NZ,CMPROM1
		LD IXL,"A"		;Ž…„…‹ˆ‹Ž‘œ, Ž˜ˆ‚Š€ „‹Ÿ €’Œ
;		JR RSTRAM

;CP_PENTEVO	LD HL,0XF000		;‡€Ž‘ ‚…‘ˆˆ FPGA
;		LD C,L
;		CALL WRITECMOS		;ˆ˜…Œ ‚ €„…‘ 0XF0 „‹Ÿ Ž‹“—…ˆŸ ‚…‘ˆˆ
;		CALL READCMOS		;—ˆ’€…Œ Ž’‚…’
;		LD A,L
;		CP C			;‘Ž‚€‹Ž ‘ ’…Œ —’Ž ‡€ˆ‘€Ž, … Ž„„…†ˆ‚€…’‘Ÿ
;		JP Z,RSTKEYS		;ˆ ’Ž … …’…‚€ (ˆ‹ˆ ‚…‘ˆŸ FPGA ‘’€€Ÿ). ‚›•Ž„ˆŒ
;		INC A			;ˆ˜…‹ 0XFF, ‡€—ˆ’ —€‘Ž‚ …’“
;		JP Z,RSTKEYS		;‚›•Ž„ˆŒ
;		LD B,0X0F		;Ž‘’€‹Ž‘œ Ž—ˆ’€’œ Ž‘’€‹œ›… …™… 15 €‰’
;GVE1		INC H
;		CALL READCMOS		;—ˆ’€…Œ €‰’
;		DJNZ GVE1		;—ˆ’€…Œ ŽŠ€ ‚‘… €‰’› … ŠŽ—€’‘Ÿ
;		LD IXL,"E"		;Ž…„…‹ˆ‹€‘œ, Ž˜ˆ‚Š€ „‹Ÿ …’…‚›
;		LD A,1
;		OUT (0XBF),A

;€ …‚ŽŒ Ž•Ž„… ‚ ‚ˆ„…Ž …†ˆŒ… 640•200
;€ ‚’ŽŽŒ Ž•Ž„… ‚ ’…Š‘’Ž‚ŽŒ …†ˆŒ…
RSTRAM		LD DE,0X1002
RSRAMP0		LD BC,0X0077
		OUT (C),E		;‚›“€…Œ „ˆ‘…’—…€ €ŒŸ’ˆ
		LD HL,TRSTRAM
RSRAMP1		LD BC,0X7FFD
		OUT (C),D		;‚“€…Œ ŠŽ”ˆƒ 48Š € …‚ŽŒ Ž•Ž„…, 128Š € ‚’ŽŽŒ
		LD BC,0X0FF7
RSRAMP2		LD A,(HL)
		OUT (C),A		;ˆ˜…Œ ŽŒ… ‘’€ˆ–› „‹Ÿ ’…Š“™…ƒŽ ŽŠ€ Ž…–ˆŽ‚€ˆŸ
		INC HL
		LD A,B
		ADD A,0X40		;‘‹…„“ž™…… ŽŠŽ Ž…–ˆŽ‚€ˆŸ
		LD B,A
		JR NC,RSRAMP2		;…‘‹ˆ … ŠŽ—ˆ‹Ž‘œ ……•Ž„ˆŒ Š ‡€ˆ‘ˆ ‚ ‘‹…„“™…… ŽŠŽ
		LD A,D
		XOR 0X10		;‘Œ…ˆ‹ˆ …†ˆŒ € 128Š
		LD D,A
		JR Z,RSRAMP1		;ˆ ……Š‹ž—…ˆˆ ‚ 128Š Ž‚’ŽŸ…Œ
		LD A,E			;ˆ€—… Ž„Ž‹†€…Œ „€‹……
		XOR 4			
		LD E,A
		CP 2
		JR NZ,RSRAMP0		;§ ç¥¬ 2 à § ? Ÿ ’Ž†… … ŽˆŒ€ž ‡€—…Œ :)
;		LD A,IXL
;		CP "A"
;		JP NZ,END_INIT
		LD A,%10101011		;ZX SCREEN MODE, TURBO ON („‹Ÿ €’Œ)
		LD BC,0X4177		;¢ëª«.PAL
		OUT (C),A		;¢ª« RAM (PEN)
;„‹Ÿ …’…‚› Ž•Ž„ ˆŽ … “†Ž ˆˆ’ˆ’œ ”€—
;”€—
		LD A,0X06
		LD BC,0X0177
		OUT (C),A
		LD HL,TFAPC
		LD DE,0X0610
		LD C,0X77
		LD A,0X08
		OUT (0XFF),A
FAPC0		LD A,(HL)
		INC HL
		OUT (0XFF),A
		LD B,0X41
		OUT (C),D
		OR 0X08
		OUT (0XFF),A
		LD B,0X01
		OUT (C),D
		DEC E
		JR NZ,FAPC0
		LD BC,0X4177
		LD A,0X26		;0X06 ¢ëª«îç¨â INT
		OUT (C),A
		LD A,0XFF
		OUT (0XFF),A
;~”€—
;„‹Ÿ €‹ˆ’› ŽŠ€ ’Ž†… Ž•Ž„
;¯ «¨âà 
		LD HL,TRSTPAL		;+15
		LD DE,0XAB0F
		LD BC,0X0177
		OUT (C),D		;¢ª«.PAL
RSTPAL0		LD A,E
		BIT 3,E
		OUT (0XFE),A
		JR Z,$+4
		OUT (0XF6),A
		LD A,(HL)
		INC HL
		OUT (0XFF),A
		DEC E
		JP P,RSTPAL0
END_INIT	LD A,%10101011
		LD BC,0X4177
		OUT (C),A		;¢ëª«.PAL
;		XOR A
;		OUT (0XBF),A
;~¯ «¨âà 

;¯à®¢¥àª  ­ ¦ â¨ï ¤¢ãå ª« ¢¨è (¯®àâ¨¬ â®«ìª® AF,BC)
RSTKEYS		LD BC,0XFEFF
RSTKEYS1	LD A,B
		IN A,(0XFE)
		REPT 5
		RRA
		JR C,$+3
		INC C
		ENDM
		RLC B
		JR C,RSTKEYS1
		DEC C
		JP P,GTSTKEY		;€†€’Ž Ž‹…… 1 ŠŽŠˆ, ……•Ž„ˆŒ Š ’…‘’“ Š‹€‚ˆ€’“›
		LD DE,0			;1 €†€’€, Ž‚…Ÿ…Œ —’Ž ’Ž
		LD BC,0X7FFD
		PUSH DE
		LD A,0XFE
		IN A,(0XFE)		;—’…ˆ… €†€’Ž‘’ˆ ŠŽŠˆ "CS"
		RRA
		LD A,0
		JP NC,START_SELECT	;ˆ €†€’Ž‰ "CS" ……•Ž„ˆŒ ‚ …‰‘ˆŠ 128
		LD A,0X7F
		IN A,(0XFE)		;—’…ˆ… €†€’Ž‘’ˆ ŠŽŠˆ "SS"
		LD D,A
		RRA
		RRA
		LD A,0X10
		JP NC,START_SELECT	;ˆ €†€’Ž‰ "CS" ……•Ž„ˆŒ ‚ …‰‘ˆŠ 48
		LD A,D
		RRA			;Ž‚…Š€ €†€’Ž‘’ˆ ŠŽŠˆ "SPACE"
		JR C,CMPCFG1		;……•Ž„ˆŒ „€‹…… …‘‹ˆ ˆ—…ƒŽ … €†€’Ž
		LD A,0X10
		LD DE,0X3D2F
		PUSH DE
		JP START_SELECT		;ˆ €†€’Ž‰ "SPACE" ……•Ž„ˆŒ ‚ TR-DOS

CMPCFG1		LD A,0XFD
		IN A,(0XFE)
		AND 4			;‡€“‘Š „…ŒŠˆ
		JP Z,GDEMO
		LD HL,DEPKADR
		LD SP,HL
		EX DE,HL
		LD HL,LOADADR		;€‘€ŠŽ‚Š€ … ƒ‹žŠ€
		CALL DEC40
		LD HL,CHARS
		LD DE,0X0F800
		CALL DEC40
		XOR A
		IN A,(0XFE)
		CPL
		AND 0X1F
		JR NZ,CMOSHELP
		LD HL,0X6000
		PUSH HL
		JR START_SELECT

GTSTKEY		SCF
		LD A,0X10
		
CMOSHELP	LD HL,BONUADR		;CMOS setup & HELP
		LD DE,0X6000		;€‘€ŠŽ‚Š€ ˆ ‡€“‘Š CMOS SETUP
		PUSH DE
		PUSH AF
		CALL DEC40
		POP AF
		JR START_SELECT

GDEMO		LD HL,0X6000-szdemoini
		LD SP,HL
		EX DE,HL
		PUSH DE
		LD HL,DEMO
		LD B,3
		LDIR

START_SELECT	PUSH AF
		LD D,A
		LD HL,0X5C07
		LD (HL),0XC9
		DEC HL
		LD (HL),0XF1
		DEC HL
		LD (HL),0X79
		DEC HL
		LD (HL),0XED
		LD A,IXL
		CP "A"
		JR Z,CMPCFG2		;…‘‹ˆ Ž…„…‹ˆ‹Ž‘œ Š€Š €’Œ, ’Ž € €’Œ ˆ ‡€“‘Š€…Œ
		LD A,D
		LD BC,0X7FFD
		JP (HL)
		
CMPCFG2		LD BC,0X0FF7
		LD A,0X81
		OUT (C),A
		LD BC,0XFF77
		LD A,0XAB
		JP (HL)
	
BONUADR		BINCLUDE cmosset_pack.rom

LOADADR		BINCLUDE main_pack.rom

CHARS		BINCLUDE chars_pack.bin

	       	DUPL 0X3D2F-$,0XFF

		ORG 0X3D2F
		NOP
		RET

DEMO		LD BC,0X7FFD
		LD A,0X10
		OUT (C),A
		EI
szdemoini	EQU $-DEMO

		BINCLUDE grass.bin

		INCLUDE dec40.a80

TRSTRAM		DB 0X83,0X7A,0X7D,0XFF,0X00,0X7A,0X7D,0XFF

TFAPC		DB 0XF1,0XE1,0XD1,0XC1,0XC1,0XB1,0XA1,0X91
		DB 0X41,0X21,0X31,0X11,0X01,0X01,0XF1,0XE1

TRSTPAL		DB 0X00,0X21,0X42,0X63,0X90,0XB1,0XD2,0XF3
		DB 0XE0,0XE1,0XE2,0XE3,0XF0,0XF1,0XF2,0XF3

;€ ‚•Ž„…: H-€„…‘ Ÿ—…‰Šˆ
;	   L-Ž—ˆ’€Ž… ‡€—…ˆ…
READCMOS	PUSH BC
		LD BC,SYSREG_EFF7
		LD A,CMOS_ON
		OUT (C),A
		LD B,HIGH (SET_ADR_CMOS)
		OUT (C),H
		LD B,HIGH (RD_WR_CMOS)
		IN L,(C)
		JR OFF_CMOS

;€ ‚•Ž„…: H-€„…‘ Ÿ—…‰Šˆ
;	   L-—’Ž ’“„€ ‡€ˆ‘€’œ
WRITECMOS	PUSH BC
		LD BC,SYSREG_EFF7
		LD A,CMOS_ON
		OUT (C),A
		LD B,HIGH (SET_ADR_CMOS)
		OUT (C),H
		LD B,HIGH (RD_WR_CMOS)
		OUT (C),L
OFF_CMOS	XOR A				;CMOS_OFF
		LD B,HIGH (SYSREG_EFF7)
		OUT (C),A
		POP BC
		RET
		
		DUPL 0X3FF8-$,0XFF

		DB "HEGLUK"
		DW DATA
