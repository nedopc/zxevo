
;LAST UPDATE: 21.05.2012 savelij

;   9   
DSKINFO		DB 0			;+0XE1-   
		DB 1			;+0XE2-   
		DB 0X16			;+0XE3- 
		DB 0			;+0XE4-   
SECFREE		DW 2544			;+0XE5-  
		DB 0X10			;+0XE7-  TRDOS
		DW 0			;+0XE8-2  0
		DUPL 9," "		;+0XEA-9  0X20
		DB 0			;+0XF3-1  0
		DB 0			;+0XF4-  
		DB "RAMDISKO"		;+0XF5- 
DSK_END

DISK_NONE	PUSH HL
		PUSH BC
		LD A,PAGE_RAMDISK
		LD BC,WIN_P1
		OUT (C),A
		LD HL,0X7FFF
		LD A,(HL)
		DEC H
		CP "R"
		JR NZ,DISK_NONE1
		LD A,(HL)
		CP "D"
DISK_NONE1	EX AF,AF'
		LD A,0XFA
		OUT (C),A
		EX AF,AF'
		POP BC
		POP HL
		RET

SET_RWPORT0	PUSH BC
		PUSH AF
		LD BC,CMOSD_SET_ADR
		LD A,0X0F
		OUT (C),A
		LD B,HIGH (CMOSD_RD_WR)
		IN A,(C)
		AND 3
		LD C,A
		IN A,(RW_PORT0)
		AND 0XF0
		OR C
		OUT (RW_PORT0),A
		POP AF
		POP BC
		RET

DOS2RST30	EX (SP),HL			;HL
		PUSH AF				;AF
		PUSH BC				;BC
		LD A,R
		JP PE,DOS2RST30_01
		LD A,R
DOS2RST30_01	DI
		PUSH AF				;RF
		PUSH HL				;ADR_RET
		PUSH DE				;DE
		IN A,(PEVO_CONF)
		LD L,A
		OR 1
		OUT (PEVO_CONF),A
		LD BC,RD_1WINA0
		IN A,(C)
		AND 0X3E
		LD BC,RD_1WINA0
		IN H,(C)
		RES 0,H
		LD B,HIGH (RD_DOS7FFD)
		IN E,(C)
		RES 4,E
		DEC B
		IN D,(C)
		LD BC,WIN_A0
		OUT (C),A
		LD B,HIGH (WIN_P0)
		XOR A
		JP ADR_SEL_ROM

EDOS2RST30	LD BC,DOS_NOEMUL
		LD A,(BC)
		AND 0X3E
		CP E
		JR Z,EDOS2RST02
		INC BC
EDOS2RST02	LD A,(BC)
		LD BC,WIN_A0
		CALL WR_BYTE_RET
		LD A,L
		OUT (PEVO_CONF),A
		POP DE
		POP HL
		POP AF
		JP PO,EDOS2RST01
		EI
EDOS2RST01	POP BC
		POP AF
		EX (SP),HL
		RET

CMP_SPECSYM	LD HL,(TRD_5CD9)
		LD A,(HL)
		CP "."
		JP NZ,FIND_KEYWORD
		POP HL
		RET

EXTEND_COM	DB 3,"VER"
		DW PRT_VERS
		DB 4,"VIRT"
		DW SET_VIRT
		DB 5,"DRIVE"
		DW DRIVE
		DB 3,"DIR"
		DW DIR
		DB 2,"CD"
		DW CD
		DB 4,"LOAD"
		DW LDFILE
		DB 5,"MOUNT"
		DW MOUNT
		DB 6,"UMOUNT"
		DW UMOUNT
		DB 0

END_EXT_COM	INC DE
		LD A,(DE)
		CP ":"
		DEC DE
		JP NZ,SINTAX_ERROR
		LD A,(DE)
		AND 0DFH
		SUB "A"
		JP C,SINTAX_ERROR
		CP 4
		JR NC,SELFATDRV
		LD (TRD_5CF6),A			;    
		LD (TRD_5D19),A			;   
		LD B,A
		LD A,(TRD_5D16)			; 	  ( #FF)
		AND 7CH
		OR B
		LD (TRD_5D16),A			; 	  ( #FF)
		RST 0X08
		DB WOUTFF
		JP ERR_OK

SELFATDRV	SUB 4
		PUSH AF
		RST 0X08
		DB Com_dev
		DB Devfind
		RST 0X08
		DB Com_dev
		DB Kol_vol
		POP AF
		CP E
		JP NC,SINTAX_ERROR
		RST 0X08
		DB Com_dev
		DB Set_vol
		JP ERR_OK

COM_DOT		CALL EXIT_IF_SINTAX
		LD HL,EXTEND_COM
NEXT_CMP_COM	LD A,1
		LD (TRD_5CEF),A
		LD DE,(TRD_5D11)
		INC DE
		LD A,(HL)
		INC HL
		AND A
		JP Z,END_EXT_COM
		LD B,A
LOC_C34		LD A,(DE)
		AND 0DFH
		CP (HL)
		JR NZ,PROPUSK
		INC DE
		INC HL
		DJNZ LOC_C34
		LD A,(DE)
		CP 0DH
		JR Z,NO_PARAM
		CP " "
		JR Z,GET_PARAM
		JR LOC_C58

PROPUSK		INC HL
		DJNZ PROPUSK
LOC_C58		INC HL
		INC HL
		JR NEXT_CMP_COM

GET_PARAM	INC DE
NO_PARAM	LD A,(HL)
		INC HL
		LD H,(HL)
		LD L,A
		PUSH HL
		LD HL,END_COMAND
		EX (SP),HL
		JP (HL)

;         
PRT_NUM_VIRT	LD HL,TXT4VIRTDRV
		LD B,ETXT4VIRTDRV-TXT4VIRTDRV-1
		CALL PRT_B_HL_
		LD A," "
		RST 0X10
		LD H,0X0F
		CALL READCMOS
		AND 3
		ADD A,"A"
		RST 0X10
		LD A,0X0D
		RST 0X10
		RET

;  
SET_VIRT	LD A,(DE)
		CP 0X0D
		JR Z,PRT_NUM_VIRT
		LD C,A
		INC DE
		LD A,(DE)
		CP ":"
		JP NZ,SINTAX_ERROR
		LD A,C
		CALL NUMDSK2BYTE
		LD H,0X0F
		LD L,A
		OUT (RW_PORT0),A
		JP WRITECMOS

;     
PRT_VERS	LD HL,0X3FF8
		LD B,6
		CALL PRT_B_HL_
		LD A," "
		RST 0X10
		LD C,(HL)
		INC HL
		LD B,(HL)
		PUSH BC
		LD HL,ZASTAVKA_VER
		LD B,9
		CALL PRT_B_HL_
		LD A," "
		RST 0X10
		POP BC
		LD A,C
		AND 0X1F			; 5 - 
		CALL A2TXT			;   
		SRL B
		RR C				;    
		LD A,"."
		RST 0X10
		LD A,C				;  
		RRCA
		RRCA
		RRCA
		RRCA				;   
		AND 0X0F			;   4  
		CALL A2TXT			;   
		LD A,"."
		RST 0X10
		LD A,"2"
		RST 0X10
		LD A,"0"
		RST 0X10
		LD A,B				;  
		AND 0X3F			;  6 
		CALL A2TXT			;   
		BIT 6,B				;  6 (  7) 
		LD HL,TXT_BETA
		RST 0X18
		LD A,0X0D
		RST 0X10
		RET

; B=   HL=   
PRT_B_HL_	LD A,(HL)
		INC HL
		RST 0X10
		DJNZ PRT_B_HL_
		RET

TXT_BETA	DC " beta"

; "A"     
A2TXT		LD L,A
		LD H,0
		LD DE,10
		XOR A
		DEC A
		INC A
		SBC HL,DE
		JR NC,$-3
		ADD HL,DE
		ADD A,"0"
		RST 0X10
		LD A,L
		ADD A,"0"
		RST 0X10
		RET

DRIVES		DC "Drives: "
EDRIVES

DRIVE		RST 8
		DB Com_dev
		DB Devfind
		RST 8
		DB Com_dev
		DB Kol_vol
		LD A,"D"
		ADD A,E
		PUSH AF
		LD HL,DRIVES
		RST 0X18
		LD A,"E"
		RST 0X10
		LD A,"-"
		RST 0X10
		POP AF
		RST 0X10
		RET

TXT_DIR		DC "<DIR> "

DIR		LD B,0
		LD A,(DE)
		INC DE
		CP "/"
		JR NZ,DIR06
		LD A,(DE)
		AND 0XDF
		CP "L"
		JR NZ,DIR06
		LD B,A
DIR06		RST 8
		DB Com_dev
		DB Devfind
		PUSH IX
		LD IXL,B
		CALL CLEAR_SCREEN
		CALL OPEN_CHAN_2
		LD A,2
		RST 8
		DB Com_fat
		DB Pos_files
DIR05		LD E,0X17
DIR01		PUSH DE
		PUSH BC
		LD HL,TRD_5D25
		RST 8
		DB Com_fat
		DB Read_dir
		LD DE,0X0B
		ADD HL,DE
		LD A,(HL)
		SBC HL,DE
		AND 0X10
;		LD IXH,A
		JR Z,DIR04
		PUSH HL
		LD HL,TXT_DIR
		RST 0X18
		POP HL
DIR04		LD A,IXL
		AND A
		JR Z,DIR08
		PUSH HL
		RST 8
		DB Com_fat
		DB Get_longname
		POP HL
DIR03		LD A,(HL)
		AND A
		JR Z,DIR02
		INC HL
		RST 0X10
		JR DIR03

DIR08		LD B,8
DIR081		LD A,(HL)
		CP " "
		JR Z,DIR082
		RST 0X10
		INC HL
		DJNZ DIR081
DIR082		LD A,B
		AND A
		JR Z,DIR085
DIR086		LD A,(HL)
		CP " "
		JR NZ,DIR085
		INC HL
		DJNZ DIR086
DIR085		
;		LD A,IXH
;		AND A
;		JR NZ,DIR084
		LD A,(HL)
		CP " "
		JR Z,DIR084
		LD A,"."
		RST 0X10
DIR084		LD B,3
DIR083		LD A,(HL)
		CP " "
		JR Z,DIR02
		RST 0X10
		INC HL
		DJNZ DIR083
DIR02		LD A,0X0D
		RST 0X10
		LD A,4
		LD B,1
		RST 0X08
		DB Com_fat
		DB Pos_files
		POP HL
		AND A
		SBC HL,BC
		POP DE
		JR Z,DIR07
		DEC E
		JR NZ,DIR01
		JR DIR05

DIR07		POP IX
		RET

LDFILE		PUSH DE
		RST 0X08
		DB Com_dev
		DB Devfind
		POP HL
		RST 0X08
		DB Com_fat
		DB Find_name
		JP C,SINTAX_ERROR
		RST 0X08
		DB Loadfile
		RET

CD		PUSH DE
		RST 0X08
		DB Com_dev
		DB Devfind
		POP HL
		RST 0X08
		DB Com_fat
		DB Find_name
		JP C,SINTAX_ERROR
		RST 0X08
		DB Com_fat
		DB Enter_dir
		RET

NOMER_DRV	RST 8
		DB Com_dev
		DB Devfind
		RST 8
		DB Com_dev
		DB Kol_vol
 
;		LD A,D
;		ADD A,"E"
;		RST 0X10

		CALL CREATE_BUF
		LD HL,TRD_5D25
		RST 8
		DB Com_fat
		DB Get_path
		CALL PRINT2ZERO
		CALL DEL_BUF

		LD HL,GET_COMMAND1+1
		EX (SP),HL
		RET

MOUNT		PUSH DE
		RST 8
		DB Com_dev
		DB Devfind
		EX DE,HL
		POP HL
		PUSH HL
		RST 8
		DB Com_fat
		DB Find_name
		JP C,SINTAX_ERROR
		POP DE
		LD B,0X0C
MOUNT02		LD A,(DE)
		CP "!"
		JR C,MOUNT01
		INC DE
		DJNZ MOUNT02
MOUNT01		LD A,(DE)
		CP 0X0D
		JP Z,SINTAX_ERROR
		CP " "
		JP NZ,SINTAX_ERROR
		INC DE
		LD A,(DE)
		CP "A"
		JP C,SINTAX_ERROR
		AND 0XDF
		CP "E"
		JP NC,SINTAX_ERROR
		SUB "A"
		LD C,A
		INC DE
		LD A,(DE)
		CP ":"
		JP NZ,SINTAX_ERROR
		LD A,C
		RST 8
		DB Mounter
		DB Open_mount
		RET

WR_RD_SEC_M	PUSH IX
		LD (TEMP_SP),SP
		LD HL,(DOS_STEK)
		LD DE,SAVED_RAM
		LD BC,0X10
		LDIR
		LD SP,HL
		IN A,(RW_PORT1)			;  
		ADD A,A				; 2
		LD D,A
		LD A,(WR_FF)
		AND 0X10			;   
		JR NZ,WRRDSECM1
		INC D				;  1
WRRDSECM1	IN A,(RW_PORT2)			;  
		LD E,A				;D-, E-
		LD HL,(REG_L)			;HL= /
		LD A,(WR_FF)
		AND 3
		LD C,A				; 
		LD A,IXL
		AND A				;BIT 7=0-
		JR Z,WRRDSECM2
		LD A,0X80			;BIT 7=1-
WRRDSECM2	OR C
		LD IXL,A			;A= + /
		LD A,(TEK_ROMPAGE)
		AND 0X3E
		LD IXH,A
		LD BC,WIN_A0
		OUT (C),A
		LD A,IXL
		RST 8
		DB Mounter
		DB Rdwr_mount
		LD BC,WIN_A0
		LD A,IXH
		OUT (C),A
		LD B,HIGH (WIN_P0)
		LD A,PAGE_EVODOS
		OUT (C),A
		LD (REG_L),HL			;  /
		LD HL,SAVED_RAM
		LD DE,(DOS_STEK)
		LD BC,0X10
		LDIR
		LD SP,(TEMP_SP)
		POP IX
		RET

UMOUNT		LD A,(DE)
		INC DE
		AND 0XDF
		LD B,A
		LD A,(DE)
		INC DE
		CP ":"
		JP NZ,SINTAX_ERROR
		LD A,B
		CP "A"
		JP C,SINTAX_ERROR
		CP "E"
		JP NC,SINTAX_ERROR
		SUB "A"
		RST 8
		DB Mounter
		DB Closemount
		RET
