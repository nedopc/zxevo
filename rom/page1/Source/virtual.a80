
;LAST UPDATE: 25.11.2010 savelij

SYSREG_EFF7	EQU 0XEEF7
SET_ADR_CMOS	EQU 0XDEF7
RD_WR_CMOS	EQU 0XBEF7

STEK		EQU 0X4010			;2   
WR_1F		EQU STEK+2			;1     1F
RD_1F		EQU WR_1F+1			;1
PORT3F		EQU RD_1F+1			;1     3F
PORT5F		EQU PORT3F+1			;1     5F
PORT7F		EQU PORT5F+1			;1     7F
WR_FF		EQU PORT7F+1			;1     FF
RD_FF		EQU WR_FF+1			;1
BUFF_SECT	EQU RD_FF+1			;2

DNO_STEK	EQU 0X40FF			; 

; 256 
COPY_BLOCK	REPT 254
		LDI
		ENDM
		JR COPY_BLOCK1
		
		DW 0XFFFF			;   IM2

COPY_BLOCK1	LDI
		LDI
ECOPY_BLOCK	LD A,0XFA			;    
		LD BC,0X77F7
		OUT (C),A			;  5  1  
		LD A,0XFD
		LD B,0XB7
		OUT (C),A			;  2  2  
		RET

WOUT1F		EQU 0X00		;00
WOUT3F		EQU 0X01		;01
WOUT5F		EQU 0X02		;02
WOUT7F		EQU 0X03		;03
WOUTFF		EQU 0X04		;04
WOUTI		EQU 0X05		;05
WIN1F		EQU 0X06		;06
WIN3F		EQU 0X07		;07
WIN5F		EQU 0X08		;08
WIN7F		EQU 0X09		;09
WINFF		EQU 0X0A		;0A
WINI		EQU 0X0B		;0B
WOUTCD		EQU 0X0C		;0C
WOUTCA		EQU 0X0D		;0D
WINHC		EQU 0X0E		;0E
WOUT1F_RET	EQU 0X0F		;0F
WINFF_JUMP	EQU 0X10		;10

W_DATA		DW W_OUT1F		;00
		DW W_OUT3F		;01
		DW W_OUT5F		;02
		DW W_OUT7F		;03
		DW W_OUTFF		;04
		DW W_OUTI		;05
		DW W_IN1F		;06
		DW W_IN3F		;07
		DW W_IN5F		;08
		DW W_IN7F		;09
		DW W_INFF		;0A
		DW W_INI		;0B
		DW W_OUTCD		;0C
		DW W_OUTCA		;0D
		DW W_INHC		;0E
		DW W_OUT1F_RET		;0F
		DW W_INFF_JUMP		;10

TEXT4VIRTDRV	DB 0X16,ZASTV_Y+2,0
		DC "Virtual Drive: "

RST08_WORK	EXX
		EX AF,AF'
		EX (SP),HL			; HL'
		PUSH AF				; AF'
		PUSH BC
		LD A,I
		PUSH AF
		LD A,0X3B
		LD I,A
		LD BC,0X77F7
		XOR A
		OUT (C),A
		LD (STEK),SP
		LD SP,DNO_STEK
		LD A,(HL)
		INC HL
		PUSH HL				; HL= 
		LD HL,EXIT_RST08
		PUSH HL				;  
		LD HL,W_DATA
		ADD A,A
		ADD A,L		
		LD L,A
		LD A,(HL)
		INC L
		LD H,(HL)
		LD L,A
		JP (HL)

EXIT_RST08	POP HL
		LD SP,(STEK)
		LD BC,0X77F7
		LD A,0XFA
		OUT (C),A
		POP AF
		LD I,A
		POP BC
		POP AF				; AF'
		EX (SP),HL			; HL'
		EX AF,AF'
		EXX
		RET

W_OUT1F_RET	CALL W_OUT1F
		RET NZ
		LD HL,FOR_RET
		LD (DNO_STEK-2),HL
		RET

; "A"   0X1F
W_OUT1F		CALL CP_TYPEDRIVE		;    
		JR NZ,W_OUT1F1
		EX AF,AF'
		LD (WR_1F),A			;     
		EX AF,AF'
		RET

W_OUT1F1	EX AF,AF'
		LD (WR_1F),A			;     
		OUT (0X1F),A			;   
		EX AF,AF'
		RET

; "A"   0X3F
W_OUT3F		EX AF,AF'
		LD (PORT3F),A
		OUT (0X3F),A
		EX AF,AF'
		RET
	
; "A"   0X5F
W_OUT5F		EX AF,AF'
		LD (PORT5F),A
		OUT (0X5F),A
		EX AF,AF'
		RET
	
; "A"   0X7F
W_OUT7F		EX AF,AF'
		LD (PORT7F),A
		OUT (0X7F),A
		EX AF,AF'
		RET

; "A"   0XFF
W_OUTFF 	EX AF,AF'			;   
		LD (WR_FF),A
		OUT (0XFF),A
		EX AF,AF'
		RET

; "A"   (C)
W_OUTCA 	EXX
		LD A,C
		EXX
		CP 0XFD
		JR NZ,W_OUTC1F
		EXX				;   TR-DOS
		EX AF,AF'			;   
		OUT (C),A
		EX AF,AF'
		EXX
		RET

;    	
W_OUTC1F	CP 0X1F
		JR Z,W_OUT1F
		CP 0X3F
		JR Z,W_OUT3F
		CP 0X5F
		JR Z,W_OUT5F
		CP 0X7F
		JR Z,W_OUT7F
		JR W_OUTFF

; "D"   (C)
W_OUTCD		CALL CP_TYPEDRIVE
		JR NZ,W_OUTCD1
		EXX 
		LD A,C
		EXX
		CP 0XFD
		JR NZ,W_OUTC1F
W_OUTCD1	EXX
		OUT (C),D
		EXX
		RET
	
; ,  OUTI
W_OUTI		CALL CP_TYPEDRIVE
		JP NZ,W_OUTI1
		LD HL,(BUFF_SECT)
		EXX
		LD A,(HL)
		INC HL
		EXX
		LD (HL),A
		INC HL
		LD (BUFF_SECT),HL
		RET

W_OUTI1		EXX
		OUTI
		EXX
		JP RET2NEWSP

;  0X1F	
W_IN1F		CALL CP_TYPEDRIVE
		JR NZ,W_IN1F1
		RET

W_IN1F1		EX AF,AF'
		IN A,(0X1F)
		EX AF,AF'
		RET

;  0X3F
W_IN3F		EX AF,AF'
		LD A,(PORT3F)
		EX AF,AF'
		RET
	
;  0X5F
W_IN5F		EX AF,AF'
		LD A,(PORT5F)
		EX AF,AF'
		RET

;  0X7F
W_IN7F		EX AF,AF'
		LD A,(PORT7F)
		EX AF,AF'
		RET

;  0XFF
W_INFF		CALL CP_TYPEDRIVE
		JR NZ,W_INFF1
W_INFF2		EX AF,AF'
		
		EX AF,AF'
		RET

W_INFF1		EX AF,AF'
		IN A,(0XFF)
		EX AF,AF'
		RET

W_INFF_JUMP	CALL CP_TYPEDRIVE
		JR Z,W_INFF2
		EX AF,AF'
		IN A,(0XFF)
		EX AF,AF'
		JR RET2NEWSP

;  "H"  ()
W_INHC		CALL CP_TYPEDRIVE
		JR NZ,W_INHC0
		EXX
		LD A,C
		EXX
		CP 0XFD
		JR NZ,W_INHC1
W_INHC0		EXX
		IN H,(C)
		EXX
		RET

;    
W_INHC1		CP 0X1F
		JR NZ,W_INHC2
		
		RET

W_INHC2		CP 0X3F
		JR NZ,W_INHC3
		LD A,(PORT3F)
		EXX
		LD H,A
		EXX
		RET
	
W_INHC3		CP 0X5F
		JR NZ,W_INHC4
		LD A,(PORT5F)
		EXX
		LD H,A
		EXX
		RET
	
W_INHC4		CP 0X7F
		JR NZ,W_INHC5
		LD A,(PORT7F)
		EXX
		LD H,A
		EXX
		RET
	
W_INHC5		LD A,(RD_FF)
		EXX
		LD H,A
		EXX
		RET
	
; INI
W_INI		CALL CP_TYPEDRIVE
		JR NZ,W_INI1
		LD HL,(BUFF_SECT)
		LD A,(HL)
		INC HL
		LD (BUFF_SECT),HL
		EXX
		LD (HL),A
		INC HL
		EXX
		RET

W_INI1		EXX
		INI
		EXX

RET2NEWSP	LD HL,(DNO_STEK-2)
		LD BC,READ_SEC-READD_SEC
		AND A
		SBC HL,BC
		LD (DNO_STEK-2),HL
		RET

;    
SV_LD_RAMDISK	PUSH HL
		CALL CP_TYPEDRIVE
		POP HL
		JP NZ,WR_NUM_TRACK		;       
		POP HL				;   
		POP HL
		POP BC
		XOR A
		OR B
		RET Z
		PUSH IX				;   
		DI
SVLDRAM1	PUSH BC
		PUSH HL
		CALL COM_04
		LD A,(TRD_5CF4)
		CALL COM_03
		CALL SETPOS_RAMDISK		;      1 
		LD A,0X10
		LD HL,TRD_5CF4
		INC (HL)			;  
		CP (HL)
		JR NZ,SVLDRAM2
		LD (HL),0			;    ,   =0
		INC HL
		INC (HL)			;   
SVLDRAM2	POP HL
		POP BC
		INC H				;    256 
		DJNZ SVLDRAM1
		POP IX				;  
		EI
		RET

;     
; :  DE=   /   ,
;		      256 
SETPOS_RAMDISK	EX DE,HL
		LD HL,TRD_5CF4			;  
		LD A,(HL)			;  
		INC HL				;    
		LD L,(HL)			;  
		LD H,0
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL			;    4 
		OR L				;  4     
		LD L,A				;     
		LD IX,(TRD_5CCE)		;  ?
						;00-, FF-,    
		ADD HL,HL
		ADD HL,HL			;    4
		LD A,H				;   
		INC A				;   16 
		LD IXH,A			;       
		LD C,0XF7			;      
		LD B,A				;    
		LD A,D				;  / 
		CP 0X80
		LD A,B				;   
		LD B,0X77			;  32 ,     1  
		JR NC,SP_RAMD1
		LD B,0XB7			;  32 ,     2  
SP_RAMD1	OUT (C),A			;  
		LD B,0X40			;     
		JR NC,SP_RAMD2
		LD B,0X80			;     
SP_RAMD2	LD A,L
		RRCA
		RRCA				;     
		ADD A,B				;   
		LD H,A
		LD L,0				;       256 
		LD A,D
		CP 0X80				;   
		JP NC,SP_RAMD3			;    
		CP 0X7F
		JP C,SP_RAMD3			;     
		LD A,E
		AND A				;     ,   
		JP Z,SP_RAMD3			;     
		LD A,IXL			;  ?
		AND A
		LD A,E				;    
		JR Z,SP_RAMD4
		EX DE,HL			;   
SP_RAMD4	NEG
		LD C,A				;    
		LD B,0
		LDIR
		NEG
		EX AF,AF'			;       
		LD A,0XFD
		LD BC,0XB7F7
		OUT (C),A			;     2  
		LD B,0X77
		LD A,IXH
		OUT (C),A			;    1  
		LD A,IXL			;  
		AND A
		JR Z,SP_RAMD5
		LD A,D				; 
		SUB 0X40			;       
		LD D,A
		JR SP_RAMD6

SP_RAMD5	LD A,H				;   
		SUB 0X40			;        
		LD H,A
SP_RAMD6	EX AF,AF'
		LD C,A				;   
		LD B,0
		LDIR
		JP ECOPY_BLOCK			;   

SP_RAMD3	LD A,IXL			;  
		AND A
		JP Z,COPY_BLOCK
		EX DE,HL			;   
		JP COPY_BLOCK

; : H- 
;	   L- 
READCMOS	PUSH BC
		LD BC,SET_ADR_CMOS
		OUT (C),H
		LD B,HIGH (RD_WR_CMOS)
		IN L,(C)
		JR OFF_CMOS

; : H- 
;	   L-  
WRITECMOS	PUSH BC
		LD BC,SET_ADR_CMOS
		OUT (C),H
		LD B,HIGH (RD_WR_CMOS)
		OUT (C),L
OFF_CMOS	POP BC
		LD A,L
		AND A
		RET

CP_TYPEDRIVE	PUSH HL
		LD H,0X0F
		CALL READCMOS
		LD A,(TRD_5CF6)
		CP L
		POP HL
		RET

SET_DRIVENAME	LD H,0X10
		CALL READCMOS
		AND 3
		LD (TRD_5D19),A
		LD (TRD_5CF6),A
		RET

FORMAT_RAM	CALL CP_TYPEDRIVE
		RET NZ
		LD A,1
		LD BC,0XBFF7
		OUT (C),A
		LD HL,0X8000
		LD DE,0X8001
		LD BC,0XFFF
		LD (HL),L
		LDIR
		CALL ECOPY_BLOCK
		XOR A
		RET
