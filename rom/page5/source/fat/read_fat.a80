
;LAST UPDATE: 02.04.2013 savelij

COM_FAT		EX AF,AF'
		CALL P5_READ_BYTE
		LD A,(NEXTBYTERST8)
		PUSH IX
		PUSH IY
		LD IYL,EXTERNAL
		JR ICOM_FAT1

;      FAT
ICOM_FAT	EX AF,AF'
		EX (SP),HL
		LD A,(HL)
		INC HL
		EX (SP),HL
		PUSH IX
		PUSH IY
		LD IYL,INTERNAL
ICOM_FAT1	LD (ERORDRV),SP
		PUSH HL
		LD HL,EXITDRV
		EX (SP),HL
		PUSH HL
		LD HL,TABLFAT
		ADD A,A
		ADD A,L
		LD L,A
		LD A,H
		ADC A,0
		LD H,A
		LD A,(HL)
		INC HL
		LD H,(HL)
		LD L,A
		EX AF,AF'
		EX (SP),HL
		RET

ERR_DRV		LD SP,(ERORDRV)
EXITDRV		POP IY
		POP IX
		RET

TABLFAT		DW INIT_FATVARS			;00   FAT
		DW READ_DIR			;01   
		DW ENTER_DIR			;02 /  
		DW GET_PATH			;03   
		DW GET_LONGNAME			;04    
		DW FIND_NAME			;05   
		DW FIND_FILEITEM		;06       
		DW SET_MASK_EXT			;07     
		DW OPEN_FILE			;08  
		DW READ_FILE			;09   
		DW INIT_TEKDIR			;0A     
		DW POS_FILES			;0B    

;  FAT
; :
;HL=   
;DE=D-  , E-  
;A=    MBR
; :
;BCDE=   
;A=   MBR  
INIT_FATVARS	PUSH HL
		LD HL,0XFFFF
		LD (LSTLOAD+2),HL
		LD (CAL_FAT),A
		LD E,D
		LD D,0
		EX DE,HL
		POP DE
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE
		INC HL
		INC HL
		INC HL
		CALL LOADZP
		LD (STARTRZ),DE
		LD (STARTRZ+2),BC		;   
		CALL LOADLST
		PUSH HL
		POP IX
		LD HL,0
		LD E,(IX+0X16)
		LD D,(IX+0X17)			;BPB_FATSZ16
		LD A,D
		OR E
		JR NZ,RDFAT01			;  FAT12/16 (BPB_FATSZ16=0)
		LD E,(IX+0X24)
		LD D,(IX+0X25)
		LD L,(IX+0X26)
		LD H,(IX+0X27)			;BPB_FATSZ32
						;    +36
RDFAT01		LD (SEC_FAT+2),HL
		LD (SEC_FAT),DE			;   FAT-
		LD HL,0
		LD E,(IX+0X13)
		LD D,(IX+0X14)			;BPB_TOTSEC16
		LD A,D
		OR E
		JR NZ,RDFAT02			;  FAT12/16 (BPB_TOTSEC16=0)
		LD E,(IX+0X20)
		LD D,(IX+0X21)
		LD L,(IX+0X22)
		LD H,(IX+0X23)			;BPB_TOTSEC32
						;    +32
RDFAT02		LD (SEC_DSC+2),HL
		LD (SEC_DSC),DE			;-   /

; ROOTDIRSECTORS
		LD E,(IX+0X11)
		LD D,(IX+0X12)			;BPB_ROOTENTCNT
		LD (ROOTZAP),DE
		LD BC,0
		LD H,B
		LD L,C
		LD A,D
		OR E
		JR Z,RDFAT03
		LD A,0X10
		CALL BCDE_A
		EX DE,HL

;  
;ROOTDIRSECTORS=((BPB_ROOTENTCNT*32)++(BPB_BYTSPERSEC-1))/BPB_BYTSPERSEC
; HL ROOTDIRSECTORS.  FAT32,  HL=0 

RDFAT03		PUSH HL				;ROOTDIRSECTORS
		LD A,(IX+0X10)			;BPB_NUMFATS
		LD (MANYFAT),A
		LD DE,(SEC_FAT)
		LD HL,(SEC_FAT+2)
		DEC A
		JR Z,RDFAT06			; FAT  1,  
RDFAT05		EX DE,HL
		ADD HL,HL
		EX DE,HL
		ADC HL,HL
		DEC A
		JR NZ,RDFAT05
RDFAT06		POP BC				;  FAT-  
		CALL HLDEPBC			; ROOTDIRSECTORS
		LD C,(IX+0X0E)
		LD B,(IX+0X0F)			;BPB_RSVDSECCNT
		LD (RSVDSEC),BC
		CALL HLDEPBC			; BPB_RESVDSECCNT
		LD (FRSTDAT),DE
		LD (FRSTDAT+2),HL		;    
		LD B,H
		LD C,L
		LD HL,SEC_DSC
		CALL BCDEHLM			;   -  
		LD A,(IX+0X0D)
		LD (BYTSSEC),A			;   
		CALL BCDE_A			;  -   
		LD (CLS_DSC),DE
		LD (CLS_DSC+2),BC		; -   

		LD A,(CAL_FAT)
		CP 0XFF
		JR NZ,RDFAT04
;  FAT   MBR
		LD DE,(SEC_FAT-1)
		LD BC,(SEC_FAT+1)
		LD E,0				;BCDE=  *0X100
		PUSH BC
		PUSH DE				;
		SRL B
		RR C
		RR D
		RR E				;BCDE=  *0X80
		LD HL,CLS_DSC			;   FAT
		PUSH HL				;
		CALL HLBCDEM			; -( *0X80)
		LD A,E
		AND 0X80			;  128     FAT32
		OR D
		OR C
		OR B
		LD A,2
		POP HL
		POP DE
		POP BC
		JR Z,RDFAT04			;FAT32   Z=0
		CALL HLBCDEM			; -( *0X100)
		LD A,D
		OR C
		OR B
		LD A,1
		JR Z,RDFAT04			;FAT16   Z=0
		XOR A				; FAT12

RDFAT04		LD (CAL_FAT),A

; FAT12/16 ROOT =0
; FAT32    +44
;  BCDE- ROOTDIR 
		PUSH AF
		AND A
		LD DE,0
		LD B,D
		LD C,E
		JR Z,FSRROO2			;FAT12
		DEC A
		JR Z,FSRROO2			;FAT16
		LD E,(IX+0X2C)
		LD D,(IX+0X2D)
		LD C,(IX+0X2E)
		LD B,(IX+0X2F)			;FAT32
FSRROO2		LD (ROOTCLS),DE
		LD (ROOTCLS+2),BC		; ROOT 
		LD (TEK_DIR),DE
		LD (TEK_DIR+2),BC
FSRR121		PUSH DE
		PUSH BC
		LD DE,(RSVDSEC)
		LD BC,0
		LD HL,STARTRZ
		CALL BCDEHLP
		LD (FATSTR),DE
		LD (FATSTR+2),BC
		XOR A
		LD (TEK_LEV),A
		LD (BUF_PATH),A
		CALL INIT_PATH
		POP BC
		POP DE
		POP AF
		RET

;   DIR
;   ""   DIR
INIRTSC		PUSH AF
		LD HL,0XFFFF
		LD (LSTLOAD+2),HL
		LD HL,BUF_TDIRCLS
		LD DE,(TEK_DIR)
		LD BC,(TEK_DIR+2)
		CALL SAVEZP
		LD A,D
		OR E
		OR B
		OR C
		JR Z,LASTCLS
NEXTCLS		PUSH HL
		CALL RDFATZP
		CALL LST_CLS
		POP HL
		JR C,LASTCLS
		CALL SAVEZP
		JR NEXTCLS

LASTCLS		LD BC,0XFFFF
		CALL SAVEZP
		CALL POSTF02
		POP AF
		RET

;   
RDFATZP		LD A,(CAL_FAT)
		AND A
		JR Z,RDFATS0
		DEC A
		JR Z,RDFATS1
		EX DE,HL
		ADD HL,HL
		EX DE,HL
		LD HL,0
		ADC HL,BC
		ADC HL,BC
		LD A,E
		LD E,D
		LD D,L
		LD C,H
		LD B,0
		CALL RDFATS2
		INC HL
		LD C,(HL)
		INC HL
		LD B,(HL)
		RET

RDFATS1		LD BC,0
		LD A,E
		LD E,D
		LD D,C
RDFATS2		PUSH AF
		PUSH BC
		LD HL,FATSTR
		CALL BCDEHLP
		CALL LOADLST
		POP BC
		POP AF
		LD E,A
		LD D,0
		ADD HL,DE
		ADD HL,DE
		LD E,(HL)
		INC HL
		LD D,(HL)
		RET

RDFATS0		LD H,D
		LD L,E
		ADD HL,HL
		ADD HL,DE
		SRL H
		RR L
		LD A,E
		LD E,H
		LD D,0
		LD B,D
		LD C,D
		SRL E
		PUSH AF
		PUSH HL
		LD HL,FATSTR
		CALL BCDEHLP
		CALL LOADLST
		POP BC
		LD A,B
		AND 1
		LD B,A
		ADD HL,BC
		LD B,(HL)
		INC HL
		LD A,H
		CP HIGH (BUF_512)+2
		JR NZ,RDFATS4
		PUSH BC
		LD BC,0
		INC DE
		CALL LOADLST
		POP BC
RDFATS4		POP AF
		LD D,(HL)
		LD E,B
		LD BC,0
		RRA
		JR NC,RDFATS3
		REPT 4
		SRL D
		RR E
		ENDM
RDFATS3		LD A,D
		AND 0X0F
		LD D,A
		RET

;  
; : BCDE=  FAT
; : BCDE= 
REALSEC		LD A,B
		OR C
		OR D
		OR E
		JR NZ,REALSE1
		LD DE,(FATSTR)
		LD BC,(FATSTR+2)
		LD HL,SEC_FAT
		PUSH HL
		CALL BCDEHLP
		POP HL
		JP BCDEHLP

REALSE1		LD HL,0XFFFE
		EX DE,HL
		ADD HL,DE
		EX DE,HL
		INC HL
		ADC HL,BC			; -2
		LD A,(BYTSSEC)
		JR REALSE2

REALSE3		SLA E
		RL D
		RL L
		RL H
REALSE2		RRCA
		JR NC,REALSE3			;   
		LD B,H
		LD C,L
		LD HL,STARTRZ
		CALL BCDEHLP			;    
		LD HL,FRSTDAT
		JP BCDEHLP			;    

;    
LSTSAVE		LD BC,(LSTLOAD+2)
		LD DE,(LSTLOAD)
		LD HL,BUF_512
		LD A,1
		CALL TO_DRV
		DB _DEV_WRITE
		AND A
		JP NZ,ERR_DRV
		RET

;   
LOADLST		CALL CPNUMSC
		JR NZ,LOADLS1
		LD HL,BUF_512
		RET

LOADLS1		LD (LSTLOAD+2),BC
		LD (LSTLOAD),DE
		LD HL,BUF_512
		PUSH HL
		LD A,1
		CALL TO_DRV
		DB _DEV_READ
		AND A
		JP NZ,ERR_DRV
		POP HL
		RET

;    
CPNUMSC		LD HL,LSTLOAD
		LD A,(HL)
		INC HL
		CP E
		RET NZ
		LD A,(HL)
		INC HL
		CP D
		RET NZ
		LD A,(HL)
		INC HL
		CP C
		RET NZ
		LD A,(HL)
		CP B
		RET

;   
LST_CLS		LD A,(CAL_FAT)			;  FAT
		AND A
		JR NZ,LST_CL1
		LD HL,0X0FF7			; FAT12
		SBC HL,DE
		RET

LST_CL1		DEC A
		JR NZ,LST_CL2
		LD HL,0XFFF7			; FAT16
		SBC HL,DE
		RET

LST_CL2		LD HL,0X0FFF
		SBC HL,BC
		RET NZ
		LD HL,0XFFF7
		SBC HL,DE
		RET

;   
READ_DIR	LD A,IYL
		AND A
		JR NZ,GETFZA3
		PUSH IY
		LD IYL,INTERNAL
		PUSH HL
		LD HL,(RREG_C)
		LD (TEK_ZAP),HL
		LD HL,GETFZA4
		EX (SP),HL
GETFZA3		LD BC,(TEK_ZAP)			;  
		LD A,B
		OR C
		JR Z,GETRZAP			;  =0,     
		PUSH BC				;  
		DEC BC				;   
		CALL GETRZAP			;
		POP BC				;  
		LD E,A				;      
GETRZAP		PUSH DE
		CALL RDDIRSC			;    
		POP DE				;  E
		INC A
		LD A,E				; /  
		RET Z				;      
		EX AF,AF'			
GETFZA1		LD A,C
		AND 0X0F			;      
		LD E,A
		LD D,0
		EX DE,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE			;   
		LD DE,0X0B
		LD A,(HL)			;    
		EX DE,HL
		ADD HL,DE
		LD L,(HL)			;   
		LD H,A				;    
		EX DE,HL
		CALL CP_TWOT			;     ".." 
		LD D,0				;   
		JR NZ,GETFZA2
		LD D,0X80			;    
GETFZA2		EX AF,AF'
		CP 0X0F				;    
		LD A,E				;    
		LD E,D				;    
		RET NZ
		INC E				;   
		RET

GETFZA4		PUSH AF
		PUSH HL
		PUSH DE
		LD DE,CPU2
		ADD HL,DE
		LD DE,(RREG_L)
		LD BC,0X20
		CALL LDIR_BYTES
		POP DE
		POP HL
		POP AF
		POP IY
		RET

;  DIR   BC
; : A=0XFF-   DIR
RDDIRSC		PUSH BC
		LD D,B
		LD E,C
		LD BC,0
		LD A,0X10
		CALL BCDE_A
		LD A,E
		PUSH AF
		LD A,(BYTSSEC)
		PUSH AF
		CALL BCDE_A
		LD HL,BUF_TDIRCLS;TDIRCLS
		EX DE,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE
		CALL LOADZP
		BIT 7,B
		JR Z,RDDIRS3
		POP BC
		POP BC
		POP BC
		XOR A
		DEC A
		RET

RDDIRS3		CALL REALSEC
		POP AF
		DEC A
		LD L,A
		POP AF
		AND L
		LD L,A
		LD H,0
		ADD HL,DE
		EX DE,HL
		LD HL,0
		ADC HL,BC
		LD B,H
		LD C,L
		CALL LOADLST
		POP BC
		XOR A
		RET

;  1 
CP_ONET		LD A,(HL)
		CP "."
		RET NZ
		INC HL
		LD A,(HL)
		DEC HL
		CP " "
		RET NZ
		XOR A
		RET

;  2 
CP_TWOT		LD A,(HL)
		CP "."
		RET NZ
		INC HL
		LD A,(HL)
		DEC HL
		CP "."
		RET NZ
		XOR A
		RET

EPOS_FILES	LD HL,(TEK_ZAP)
		LD (RREG_C),HL
		RET

POS_FILES	LD D,A
		LD A,IYL
		LD IYL,INTERNAL
		AND A
		LD A,D
		JR NZ,POSITFILE1
		LD HL,EPOS_FILES
		PUSH HL
		LD A,(RREG_A)
		LD BC,(RREG_C)
POSITFILE1	CP LOW ((ENDTPOS-TPOSITF)/2)
		RET NC
		ADD A,A
		LD DE,TPOSITF
		LD L,A
		LD H,0
		ADD HL,DE
		LD E,(HL)
		INC HL
		LD D,(HL)
		EX DE,HL
		JP (HL)

TPOSITF		DW POSTF00			;   
		DW POSTF01			;   
		DW POSTF02			;    0    "" 
		DW POSTF03			;  "B"  
		DW POSTF04			;  "B"  
		DW POSTF05			;  "" 
		DW POSTF06			;  ""   "BC"
		DW POSTF07			;  "BC"   "" 
ENDTPOS

;   
POSTF00		LD HL,(TEK_ZAP)
		LD (SAVEZAP),HL
		RET

;   
POSTF01		LD HL,(SAVEZAP)
		LD (TEK_ZAP),HL
		RET

;  "B"  
POSTF03		LD A,B
		AND A
		RET Z
		PUSH BC
		CALL PRVLEGZ
		POP BC
		DJNZ $-5
		RET

;  "B"  
POSTF04		LD A,B
		AND A
		RET Z
		PUSH BC
		CALL NXTLEGZ
		POP BC
		DJNZ $-5
		RET

;  "" 
POSTF05		LD BC,0
		PUSH BC
		EXX
		POP BC
		EXX
		CALL RDDIRSC
		LD DE,0X20
KOL_ZA4		LD A,(HL)
		CP "."
		JR NZ,KOL_ZA1+2
		INC HL
		LD A,(HL)
		DEC HL
		CP "."
		JR Z,KOL_ZA1+2
		INC BC
		ADD HL,DE
		JR KOL_ZA4

KOL_ZA1		INC BC
		ADD HL,DE
		LD A,H
		CP HIGH (BUF_512)+2
		JR NZ,KOL_ZA2
		CALL RDDIRSC
		LD D,0
		AND A
		JR Z,KOL_ZA2
		CALL KOL_ZA3
		DEC A
		RET

KOL_ZA2		LD E,0X0B
		ADD HL,DE
		LD A,(HL)
		AND A
		SBC HL,DE
		LD E,0X20
		CP 0X0F
		JR Z,KOL_ZA1
		CP 8
		JR Z,KOL_ZA1
		LD A,(HL)
		CP 0XE5
		JR Z,KOL_ZA1
		AND A
		JR Z,KOL_ZA3
		EXX
		INC BC
		EXX
		JR KOL_ZA1

KOL_ZA3		LD D,B
		LD E,C
		EXX
		PUSH BC
		EXX
		POP BC
		XOR A
		RET

;  ""   "BC"
POSTF06		LD (TEK_ZAP),BC
		RET

;  "BC"   "" 
POSTF07		LD BC,(TEK_ZAP)
		RET

CPTEKZP		CALL RDDIRSC
		RET M
		LD A,C
		AND 0X0F
		LD L,A
		LD H,0
		LD DE,BUF_512
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE
		LD DE,0X0B
		ADD HL,DE
		LD A,(HL)
		SBC HL,DE
		LD E,(HL)
		AND A
		RET

;    0    "" 
POSTF02		LD BC,0
		LD (TEK_ZAP),BC
		CALL RDDIRSC
		CALL CP_ONET
		JR Z,NXTLEG1
		LD A,(HL)
		AND A
		RET Z
		JR NXTLEG1+1

NXTLEGZ		LD BC,(TEK_ZAP)
NXTLEG1		INC BC
		CALL CPTEKZP
		CP 0XFF
		RET Z
		CP 0X0F
		JR Z,NXTLEG1
		CP 8
		JR Z,NXTLEG1
		LD A,E
		AND A
		RET Z
		CP 0XE5
		JR Z,NXTLEG1
		LD (TEK_ZAP),BC
		RET

PRVLEGZ		LD BC,(TEK_ZAP)
		CALL CPTEKZP
		CALL CP_TWOT
		RET Z
PRVLEG1		DEC BC
		CALL CPTEKZP
		CP 0X0F
		JR Z,PRVLEG1
		CP 8
		JR Z,PRVLEG1
		LD A,E
		CP 0XE5
		JR Z,PRVLEG1
		LD (TEK_ZAP),BC
		RET

SAVEZP		LD (HL),E
		INC HL
		LD (HL),D
		INC HL
		LD (HL),C
		INC HL
		LD (HL),B
		INC HL
		RET

LOADZP		LD E,(HL)
		INC HL
		LD D,(HL)
		INC HL
		LD C,(HL)
		INC HL
		LD B,(HL)
		INC HL
		RET

BCDE200		LD E,D
		LD D,C
		LD C,B
		LD B,0
		LD A,2
		JR BCDE_A

;BCDE>>A=BCDE
BCDE_A1		SRL B
		RR C
		RR D
		RR E
BCDE_A		RRCA
		JR NC,BCDE_A1
		RET

;BCDE-(ADR)=BCDE
HLBCDEM		LD A,E
		SUB (HL)
		INC HL
		LD E,A
		LD A,D
		SBC A,(HL)
		INC HL
		LD D,A
		LD A,C
		SBC A,(HL)
		INC HL
		LD C,A
		LD A,B
		SBC A,(HL)
		LD B,A
		RET

;(ADR)-BCDE=BCDE
BCDEHLM		LD A,(HL)
		INC HL
		SUB E
		LD E,A
		LD A,(HL)
		INC HL
		SBC A,D
		LD D,A
		LD A,(HL)
		INC HL
		SBC A,C
		LD C,A
		LD A,(HL)
		SBC A,B
		LD B,A
		RET

;(ADR)+BCDE=BCDE
BCDEHLP		LD A,(HL)
		INC HL
		ADD A,E
		LD E,A
		LD A,(HL)
		INC HL
		ADC A,D
		LD D,A
		LD A,(HL)
		INC HL
		ADC A,C
		LD C,A
		LD A,(HL)
		ADC A,B
		LD B,A
		RET

;HLDE+BC=HLDE
HLDEPBC		EX DE,HL
		ADD HL,BC
		EX DE,HL
		LD BC,0
		ADC HL,BC
		RET

INIT_PATH	LD BC,WIN_P1
		LD A,PAGE_MOUNTER
		OUT (C),A
		LD A,(SETDVOL)
		LD D,A
		ADD A,HIGH (TEK_BUFPATH+CPU1)+4
		LD H,A
		LD L,0
		LD A,"E"
		ADD A,D
		LD (HL),A
		INC L
		LD (HL),":"
		INC L
		LD (HL),"/"
		INC L
		LD (HL),0
		INC L
		LD A,L
		LD L,0XFF
		LD (HL),A
		RET

RENEW_PATH	LD BC,WIN_P1
		LD A,PAGE_MOUNTER
		OUT (C),A
		BIT 7,E
		JR NZ,MINUS_PATH
;     (  )
		EX DE,HL
		LD A,(SETDVOL)
		ADD A,HIGH (TEK_BUFPATH+CPU1)+4
		LD H,A
		LD L,0XFF
		LD L,(HL)
		EX DE,HL
		DEC DE
		LD BC,8
		LDIR
		LD B,8
RENEWPATH3	DEC DE
		LD A,(DE)
		INC DE
		CP " "
		JR NZ,RENEWPATH2
		DEC DE
		DJNZ RENEWPATH3
RENEWPATH2	LD A,(HL)
		CP " "
		JR Z,RENEWPATH4
		LD A,"."
		LD (DE),A
		INC DE
		LD BC,3
		LDIR
		LD B,3
RENEWPATH5	DEC DE
		LD A,(DE)
		INC DE
		CP " "
		JR NZ,RENEWPATH4
		DEC DE
		DJNZ RENEWPATH5
RENEWPATH4	EX DE,HL
		LD (HL),"/"
		INC L
		LD (HL),0
		INC L
		LD A,L
		LD L,0XFF
		LD (HL),A
		RET

;   (   )
MINUS_PATH	LD A,(SETDVOL)
		ADD A,HIGH (TEK_BUFPATH+CPU1)+4
		LD H,A
		LD L,0XFF
		LD L,(HL)
		DEC L
		DEC L
MINUS_PATH1	DEC L
		LD A,(HL)
		CP "/"
		JR NZ,MINUS_PATH1
		INC L
		LD (HL),0
		INC L
		LD A,L
		LD L,0XFF
		LD (HL),A
		RET

;  
GET_PATH	LD BC,WIN_P1
		LD A,PAGE_MOUNTER
		OUT (C),A
		LD A,IYL
		AND A
		JR NZ,GETPATH1
		LD A,(SETDVOL)
		LD D,A
		ADD A,HIGH (TEK_BUFPATH+CPU1)+4
		LD H,A
		LD L,0XFF
		LD A,(HL)
		LD DE,BUF_256
		LD BC,0X100
		LD L,C
		LDIR
		LD C,A				;BC=      
		LD A,HIGH (CPU2)-1
		ADD A,D
		LD H,A
		LD DE,(RREG_L)			;  
		JP LDIR_BYTES

GETPATH1	PUSH HL
		PUSH HL
		LD A,(SETDVOL)
		LD D,A
		ADD A,HIGH (TEK_BUFPATH+CPU1)+4
		LD H,A
		LD L,0XFF
		LD A,(HL)
		POP DE
		LD BC,0X100
		LD L,C
		LDIR
		POP HL
		RET

;  DIR
ENTER_DIR	LD IYL,INTERNAL
		CALL READ_DIR
		PUSH AF
		PUSH DE
		PUSH HL
		CALL RENEW_PATH
		POP HL
		POP DE
		POP AF
		BIT 4,A
		EX AF,AF'
		LD A,E
		EX AF,AF'
		LD E,A
		LD D,(HL)
		RET Z
		EXX
		EX AF,AF'
		BIT 7,A
		JR Z,ENT_DI2
		LD HL,TEK_LEV
		LD A,(HL)
		AND A
		EXX
		RET Z
		EXX
		DEC A
		LD (HL),A
		ADD A,A
		LD H,HIGH (BUF_DIRCEP+CPU2)
		LD L,A
		LD A,(HL)
		INC L
		LD H,(HL)
		LD L,A
		JR ENT_DI1

ENT_DI2		LD HL,TEK_LEV
		LD A,(HL)
		INC A
		CP 0X10
		EXX
		RET Z
		EXX
		LD (HL),A
		DEC A
		ADD A,A
		LD H,HIGH (BUF_DIRCEP+CPU2)
		LD L,A
		LD BC,(TEK_ZAP)
		LD (HL),C
		INC L
		LD (HL),B
ENT_DI1		EXX
		PUSH DE
		LD DE,0X14
		ADD HL,DE
		LD C,(HL)
		INC HL
		LD B,(HL)
		LD E,5
		ADD HL,DE
		LD E,(HL)
		INC HL
		LD D,(HL)
		LD A,B
		OR C
		OR D
		OR E
		JR NZ,ENT_DI0
		LD DE,(ROOTCLS)
		LD BC,(ROOTCLS+2)
ENT_DI0		LD (TEK_DIR),DE
		LD (TEK_DIR+2),BC
		POP DE
INIT_TEKDIR	PUSH DE
		PUSH IY
		LD IYL,INTERNAL
		CALL INIRTSC
		POP IY
		POP DE
		RET

;   ,   
NO_LNG		POP DE
		LD BC,8
		LDIR
		EX DE,HL
NO_LNG2		DEC HL
		LD A,(HL)
		CP " "
		JR Z,NO_LNG2
		INC HL
		LD A,(DE)
		EX DE,HL
		CP " "
		JR Z,NO_LNG1
		EX DE,HL
		LD (HL),"."
		INC HL
		EX DE,HL
		LDI
		LDI
		LDI
NO_LNG1		EX DE,HL
		LD (HL),0
		LD A,L
		LD (RREG_A),A
		POP HL
		RET

GETLNG8		POP IY
		LD DE,CPU2
		ADD HL,DE
		LD DE,(RREG_L)
		LD BC,0X100
		JP LDIR_BYTES

;   
GET_LONGNAME	LD A,IYL
		AND A
		JR NZ,GETLNG9
		PUSH IY
		LD IYL,INTERNAL
		LD HL,GETLNG8
		PUSH HL
		LD HL,BUF_256
GETLNG9		PUSH HL
		PUSH HL
		CALL READ_DIR
		BIT 0,E
		JR Z,NO_LNG
		EXX
		LD C,0
		EXX
GETLNG2		DEC BC
		CALL GETRZAP
		LD A,(HL)
		INC HL
		EXX
		BIT 6,C
		POP HL
		JR NZ,GETLNGE+1
		LD C,A
		EXX
		LD A,5
		CALL GETLNG1
		AND A
		JR Z,GETLNGE
		INC HL
		INC HL
		INC HL
		LD A,6
		CALL GETLNG1
		AND A
		JR Z,GETLNGE
		INC HL
		INC HL
		LD A,2
		CALL GETLNG1
		AND A
		JR Z,GETLNGE
		EXX
		PUSH HL
		EXX
		JR GETLNG2

GETLNGE		EXX
		LD (HL),0
		LD A,L
		LD (RREG_A),A
		EXX
		POP HL
		RET

GETLNG1		EXX
		LD B,A
		EXX
		LD D,(HL)
		INC HL
		LD E,(HL)
		INC HL
		LD A,D
		OR E
		RET Z
		LD A,E
		AND A
		JR NZ,GETLNG3
		LD A,D
		CP 0X80
		JR C,GETLNG5
		LD D,0X5F
		JR GETLNG5

GETLNG3		CP 4
		LD A,0X5F
		JR NZ,GETLNG5
		LD A,D
		LD E,0XEF
		LD D,0X5F
		CP 1
		JR Z,GETLNG4
		LD E,0XA0
		CP 0X51
		JR Z,GETLNG4
		SUB 0X10
		LD E,0X80
		JR NC,GETLNG6
		LD A,D
		JR GETLNG5

GETLNG6		CP 0X30
		JR C,GETLNG4
		LD E,0XB0
		CP 0X40
		JR C,GETLNG4
		LD A,D
		JR GETLNG5

GETLNG4		ADD A,E
GETLNG5		EXX
		LD (HL),A
		INC HL
		DEC B
		EXX
		RET Z
		JR GETLNG1+3
;401,410-44F,451

; :   DE= 
;	     HL=  
; :  A= 
CP_EXT		LD C,0
CPETR2		LD A,(DE)
		AND A
		JR Z,CPETR1
		INC C
		PUSH DE
		PUSH HL
		CALL COMPARF
		POP HL
		POP DE
		LD A,C
		RET Z
		INC DE
		INC DE
		INC DE
		JR CPETR2

CPETR1		DEC A
		RET

COMPARF		PUSH DE
		LD DE,8
		ADD HL,DE
		POP DE
COMPARF1	LD A,(DE)
		CP (HL)
		RET NZ
		INC HL
		INC DE
		LD A,(DE)
		CP (HL)
		RET NZ
		INC HL
		INC DE
		LD A,(DE)
		CP (HL)
		RET

FIND_FILEITEM	PUSH IY
		LD IYL,INTERNAL
		CALL INIRTSC
		LD BC,WIN_P3
		LD A,PAGE_TEMP
		OUT (C),A
		LD HL,0
		LD (KOLFIND),HL			; 
		LD HL,CPU3
		LD (ADR_SKLAD),HL		; 
		LD HL,CPU3+0X1000
		LD (ADR_SKLAD_NAME),HL		;   2-   
		CALL READ_DIR			; 
FINDF2		LD A,(HL)
		AND A
		JR Z,FINDF3
		CP 0XE5				;  
		JR Z,FINDF2			;    
		LD DE,0X0B			;   
		ADD HL,DE			;   
		LD A,(HL)			;  
		SBC HL,DE
		AND 0X10			;    
		JR NZ,FINDF1
		LD A,(EXT4FOUND)
		AND A
		PUSH DE
		LD DE,EXT4FOUND
		PUSH BC
		CALL NZ,CP_EXT
		POP BC
		POP DE
		JR NZ,FINDF4
FINDF1		EX DE,HL
		ADD HL,DE
		LD A,(HL)
		EX AF,AF'
		LD HL,(ADR_SKLAD)		;     
		LD (HL),C
		INC HL
		LD (HL),B			;    
		INC HL
		LD (ADR_SKLAD),HL
		LD HL,(KOLFIND)
		INC HL				;+1  
		LD (KOLFIND),HL
		LD HL,(ADR_SKLAD_NAME)
		EX AF,AF'
		LD (HL),A
		INC HL
		LD A,(DE)
		LD (HL),A
		INC HL
		LD (ADR_SKLAD_NAME),HL
FINDF4		CALL NXTLEGZ
		LD A,(ADR_SKLAD+1)
		CP HIGH (CPU3)+0X10
		JR C,FINDF2			;  2048 /   
FINDF3		LD HL,(KOLFIND)
		POP IY
		LD A,IYL
		AND A
		JR NZ,FINDF5
		LD A,PAGE_TEMP
		LD (RREG_A),A
		LD (RREG_C),HL
FINDF5		LD (KOL_FOUNDED),HL
		LD B,H
		LD C,L
		XOR A
		RET

SET_MASK_EXT	LD A,(RREG_A)
		AND A
		JR NZ,SET_MASK_EXT1
		LD (EXT4FOUND),A
		RET

SET_MASK_EXT1	LD HL,(RREG_L)
		LD DE,BUF_256-CPU2
		LD BC,0X10*3+1
		CALL LDIR_BYTES
		LD HL,BUF_256
		LD DE,EXT4FOUND
		LD BC,0X10FF
SET_MASK_EXT2	LD A,(HL)
		AND A
		JR Z,SET_MASK_EXT3
		LDI
		LDI
		LDI
		DJNZ SET_MASK_EXT2
SET_MASK_EXT3	XOR A
		LD (DE),A
		RET

LD_FILE6	PUSH AF
		POP HL
		LD (RREG_F),HL
LD_FILE8	EXX
		LD (SAVE_IY),DE		;IY
		EXX
		RET

;IXL-L-   
;IXH-H- 
;IYL-E-
;IYH-D-  
READ_FILE	EX AF,AF'
		LD A,IYL
		DEC A
		JR Z,LD_FILE00
		LD IYL,INT4EXT
		LD HL,LD_FILE6
		PUSH HL
		LD HL,(RREG_L)			; 
		EXX
		LD DE,(SAVE_IY)			;  
		LD A,(BYTSSEC)
		LD H,A				;   
		LD A,(RREG_A)
		LD L,A				;   
		EXX
		JR LD_FILE0

LD_FILE00	EXX
		LD DE,(SAVE_IY)			;  
		LD HL,LD_FILE8
		PUSH HL
		LD A,(BYTSSEC)			;   
		LD H,A				;   
		EX AF,AF'
		LD L,A				;   
		EXX
;IXL-L-   
;IXH-H- 
;IYL-E-
;IYH-D-  
LD_FILE0	LD DE,(AFILCLS)
		LD BC,(AFILCLS+2)		;  
		PUSH BC
		PUSH DE
		PUSH HL
		CALL REALSEC			;     
		EXX
		LD A,D			;IYH
		EXX
		LD L,A
		LD H,0
		ADD HL,DE
		EX DE,HL
		JR NC,LD_FILE1
		INC BC				;BCDE=   
LD_FILE1	EXX
		LD A,L			;IXL
		CP H			;IXH
		JP C,LD_FILE2
		LD A,H			;IXH
LD_FILE2	ADD A,D			;IYH
		CP H			;IXH
		LD A,L			;IXL
		JP C,LD_FILE5
		LD A,H			;IXH
		SUB D			;IYH
LD_FILE5	LD E,A			;IYL,A	;   
		EXX
		POP HL				;  
;		LD IYL,INT4EXT
		CALL TO_DRV
		DB _DEV_READ
		LD A,IYL
		DEC A
		JR Z,LD_FILE7
		LD (RREG_L),HL
LD_FILE7	POP DE
		POP BC				;  
		EXX
		LD A,D			;IYH
		ADD A,E			;IYL
		CP H			;IXH
		JP C,LD_FILE3
		SUB H			;IXH
LD_FILE3	LD D,A			;IYH,A
		EXX
		JP C,LD_FILE4
		PUSH HL				;  
		PUSH IY
		LD IYL,INTERNAL
		CALL RDFATZP			;   
		POP IY
		LD (AFILCLS),DE
		LD (AFILCLS+2),BC
		CALL LST_CLS			;,     ?
		POP HL				;  
		RET C				; , 
LD_FILE4	EXX
		LD A,L			;IXL
		SUB E			;IYL
		EXX
		RET Z
		EXX
		LD L,A			;IXL,A
		EXX
		JP NZ,LD_FILE0
		RET

OPEN_FILE	LD A,IYL
		AND A
		JR NZ,OPENFILE1
		LD HL,EOPEN_FILE
		PUSH HL
OPENFILE1	PUSH IY
		LD IYL,INTERNAL
		CALL READ_DIR
		LD DE,FILE_SRC
		LD BC,0X20
		PUSH DE
		LDIR
		LD HL,0
		LD A,(CAL_FAT)
		CP 2
		JR C,OPENFILE2			;   FAT32   16   
		LD HL,(FILE_SRC+0X14)
OPENFILE2	LD (AFILCLS+2),HL
		LD HL,(FILE_SRC+0X1A)
		LD (AFILCLS),HL
		LD HL,(TEK_ZAP)
		LD (NUM_FILE_SRC),HL
		XOR A
		LD H,A
		LD L,A
		LD (SAVE_IY),HL
		POP HL
		POP IY
		RET

EOPEN_FILE	PUSH AF
		POP HL
		LD (RREG_F),HL
		RET

EFINDNAME	LD HL,(TEK_ZAP)
		LD (RREG_C),HL
		PUSH AF
		POP HL
		LD (RREG_F),HL
		RET

FIND_NAME	LD A,IYL
		AND A
		JR NZ,FINDNAME01
		PUSH HL
		LD HL,(RREG_L)
		LD DE,BUF_256-CPU2
		LD BC,0X10
		CALL LDIR_BYTES
		LD HL,EFINDNAME
		EX (SP),HL
FINDNAME01	PUSH IY
		LD IYL,INTERNAL
		LD HL,BUF_256
		LD DE,FB_EXT
		LD BC,0X8FF
		LD A,(HL)
		CP "."
		JR NZ,FINDNAME02
		LDI
		LDI
		LD B,6
FINDNAME02	LD A,(HL)
		CP "!"
		JR C,FINDNAME03
		CP "."
		JR Z,FINDNAME12
		LDI
		DJNZ FINDNAME02
		LD A,(HL)
		CP "!"
		JR C,FINDNAME06
		INC HL
		JR FINDNAME06

FINDNAME12	INC HL
FINDNAME03	LD A," "
FINDNAME05	LD (DE),A
		INC DE
		DJNZ FINDNAME05
FINDNAME06	LD B,3
FINDNAME08	LD A,(HL)
		CP "!"
		JR C,FINDNAME07
		LDI
		DJNZ FINDNAME08
		CALL POSTF02
		JR FINDNAME11

FINDNAME07	LD A," "
FINDNAME09	LD (DE),A
		INC DE
		DJNZ FINDNAME09
		CALL POSTF02
		JR FINDNAME11

FINDNAME10	CALL NXTLEGZ
FINDNAME11	PUSH HL
		CALL CMP_NAME
		POP HL
		JR Z,FINDNAME13
		LD A,(HL)
		AND A
		JR NZ,FINDNAME10
		SCF
FINDNAME13	POP IY
		RET

CMP_NAME	LD DE,FB_EXT
ICMP_NAME	LD B,0X0B
CMP_NAME_B	LD A,(DE)
		CP "a"
		JR C,CMPNAME01
		CP "{"
		JR NC,CMPNAME01
		AND 0XDF
CMPNAME01	CP (HL)
		RET NZ
		INC DE
		INC HL
		DJNZ CMP_NAME_B
		XOR A
		RET
