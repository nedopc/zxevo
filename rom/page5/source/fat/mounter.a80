
;LAST UPDATE: 17.07.2014 savelij

;32    
;00...	|  
;...1F	|  
;+20	1 
;	 7-
;	 6-
;	 5-
;	 4-
;	 3-
;	 2-
;	 1-
;	 0-
;+21	1    
;+22	1  
;+23	1  
;+24	1   
;+25	2        
;+27	2      
;+28	1   

MOUNTER		CALL P5_READ_BYTE
		LD A,(NEXTBYTERST8)
		ADD A,A
		LD E,A
		LD D,0
		LD HL,TAB_MOUNTER
		ADD HL,DE
		LD A,(HL)
		INC HL
		LD H,(HL)
		LD L,A
		JP (HL)
		
TAB_MOUNTER	DW OPEN_MOUNT			;00  
		DW RDWR_MOUNT			;01 /  
		DW FIND_MOUNTED			;02      IMAGE.FNT
		DW GET_MOUNTED			;03     
		DW CLOSEMOUNT			;04  
		DW LOADIMAGE			;05    
		DW STOR_NAMELOAD		;06      
		DW REST_NAMELOAD		;07      
		DW CMP_DRIVE			;08      

;   
LOADIMAGE	LD IYL,INTERNAL
		CALL OPEN_FILE
		PUSH HL
		LD DE,SUPPORT_EXT
		CALL CP_EXT
		LD (EXT_TYPE),A
		LD HL,FILE_SRC
		LD DE,NAME_RAMDISK
		LD BC,0X20
		LDIR
		POP HL
		CP _TAP
		JR Z,LOAD_TAPE
		CP _FDI
		JP Z,LOAD_FDI
		CP _SCL
		JP Z,LOAD_SCL
		LD A,PAGE_DATARAMD
ILD_IMG1	PUSH AF
		LD BC,WIN_P5
		OUT (C),A
		LD A,0X20
		LD HL,CPU5
		CALL READ_FILE
		JR C,ILD_IMG2
		POP AF
		INC A
		JR ILD_IMG1

LOAD_TAPE	LD A,PAGE4TAP
		JR ILD_IMG1

ILD_IMG2	POP AF
		EVOPORT WIN_P5,PAGE_DATARAMD	;     
ILD_IMG3	LD HL,(CPU5+0X8E1)		;   
		LD A,L
		LD L,H
		LD H,0
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		OR L
		LD L,A				;    
		LD DE,(CPU5+0X8E5)		;  
		ADD HL,DE
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL			;H= 
		LD A,H
		CP 0XA0
		JR NC,ELT3
ELT4		LD A,0XA0
ELT3		EX AF,AF'
		EVOPORT WIN_P5,PAGE_RAMDISK	;   ,    
		LD HL,CPU5
		PUSH HL
		LD DE,CPU5+1
		LD BC,0X3FFF
		LD (HL),L
		LDIR				; 
		POP DE				;  
		LD HL,0				;    
		EX AF,AF'
ELT2		EX AF,AF'
		LD BC,0X1000			;     
		LD A,L
		RRCA
		RRCA
		LD (DE),A			;     
		INC D
		LD A,H
		LD (DE),A			;    
		INC D
ELT1		INC C
		LD A,C
		LD (DE),A			; 
		INC D
		LD A,2
		LD (DE),A			; 
		INC D
		DJNZ ELT1			;       
		LD D,HIGH (CPU5)		;   
		INC E				;  
		LD BC,0X40
		ADD HL,BC			;   
		EX AF,AF'
		DEC A
		JR NZ,ELT2			;   
;           1
ELOAD_IMAGE	LD HL,CPU5+0X3FFF
		LD (HL),"R"
		DEC H
		LD (HL),"D"
		XOR A
		RET

ELT5		POP AF
		EVOPORT WIN_P5,PAGE_RAMDISK
		JR ELOAD_IMAGE

;   9   
DSKINFO		DB 0			;+0XE1-   
		DB 1			;+0XE2-   
		DB 0X16			;+0XE3- 
		DB 0			;+0XE4-   
SECFREE		DW 2544			;+0XE5-  
		DB 0X10			;+0XE7-  TRDOS
		DW 0			;+0XE8-2  0
		DUPL 9,0X20		;+0XEA-9  0X20
		DB 0			;+0XF3-1  0
		DB 0			;+0XF4-  
		DB "RAMDISKO"		;+0XF5- 
DSK_END

LOAD_SCL	EVOPORT WIN_P5,PAGE_DATARAMD
		EVOPORT WIN_P3,PAGE_MOUNTER
		LD A,PAGE_DATARAMD
LOAD_IMAGE4	PUSH AF
		LD BC,WIN_P5
		OUT (C),A
		LD HL,CPU5+0X1000
		LD A,0X18
		CALL READ_FILE
		PUSH AF				;       
		LD HL,CPU5
		PUSH HL				;   
		LD (HL),L
		LD D,H
		LD E,L
		INC DE
		LD BC,0XFFF
		LDIR				;       
		LD HL,CPU5+0X1008		;    SCL 
		LD A,(HL)			;    
		INC HL				;    
		EXX
		LD HL,0				;  
		LD D,L				;      255 
		LD B,A				;  
		EXX
		POP DE				;    
LOAD_IMAGE3	EX AF,AF'
		LD BC,0X0D
		LDIR				; 13   
		LD A,(HL)			;    
		LDI				;  
		EXX
		LD E,A
		PUSH HL				;    
		ADD HL,DE			;       
		EXX
		EX (SP),HL
		LD A,L
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		INC H
		AND 0X0F
		LD L,A
		EX (SP),HL
		POP BC				;   
		EX DE,HL
		LD (HL),C
		INC HL
		LD (HL),B			;   
		INC HL
		EX DE,HL
		EX AF,AF'
		DEC A
		JR NZ,LOAD_IMAGE3		;    
		PUSH HL				;   
		LD HL,DSKINFO
		LD DE,CPU5+0X8E1
		LD BC,DSK_END-DSKINFO
		LDIR
		EXX
		LD DE,2544			;  
		EX DE,HL
		AND A
		SBC HL,DE
		LD (CPU5+0X8E5),HL		;  
		EX DE,HL
		LD A,L
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		INC H
		AND 0X0F
		LD L,A
		LD (CPU5+0X8E1),HL		;    
		LD A,B
		LD (CPU5+0X8E4),A		;   
		EXX
		POP DE				;   
		LD HL,CPU6			;  
		AND A
		SBC HL,DE			;    
		LD B,H
		LD C,L
		LD HL,CPU5+0X1000		;   
		EX DE,HL
		LDIR				;,   DE=   
		LD A,D
		CP HIGH (CPU6)-2
		JR NC,LOAD_IMAGE7
		EX DE,HL
		LD A,HIGH (CPU6)-1
		SUB H
		SRL A
		LD IXL,A
		CALL READ_FILE
		LD DE,0X8000
		EX DE,HL
LOAD_IMAGE7	AND A
		SBC HL,DE			;      
		LD (OST_HWOST),HL
		LD (KUDA_HWOST),DE
		LD DE,0
		EX DE,HL
		SBC HL,DE
		LD A,1
		AND H
		LD H,A
		LD (NEXT4PAGE),HL
		POP AF				;   
		JP C,ILD_IMG2			;   ,  
		JR LOAD_IMAGE5

LOAD_IMAGE6	LD DE,CPU5
		LD BC,(NEXT4PAGE)
		LDIR
		EX AF,AF'
		JP C,ILD_IMG2
		EX DE,HL
		LD A,0X1F
		CALL READ_FILE
		JP C,ILD_IMG2
LOAD_IMAGE5	LD HL,CPU3+BUF_TEMPSEC
		PUSH HL
		LD A,1
		CALL READ_FILE
		EX AF,AF'			;   
		POP HL				;      
		LD DE,(KUDA_HWOST)		;    
		LD BC,(OST_HWOST)		;    
		LDIR
		POP AF
		INC A
		PUSH AF
		LD BC,WIN_P5
		OUT (C),A
		JR LOAD_IMAGE6

LOAD_FDI	LD A,PAGE_RAMDISK		;  
		PUSH AF				;  
		EVOPORT WIN_P5,PAGE_RAMDISK
		EVOPORT WIN_P3,PAGE_MOUNTER
		LD HL,FDI_TABLE_CYL
		LD DE,FDI_TABLE_CYL+1
		LD BC,0X3FFF
		LD (HL),0
		LDIR				;    
		LD IXH,0XFF
		CALL LOAD1SECFDI		;   
		LD A,(CPU3+6)			;  
		DEC A
		LD A,(CPU3+4)			;  
		JR Z,LOADFDI1
		ADD A,A				;  2,      2
LOADFDI1	LD C,A				; 
		LD HL,(CPU3+0X0A)
		LD (START_DATA),HL
		LD DE,CPU5			;    
		LD HL,CPU3+0X0F			;      FDI 
LOADFDI3	BIT 1,H
		CALL NZ,LOAD1SECFDI		;            
		LD A,(HL)			; 8-15    
		AND 0X3F			;  8-13 -    
		LD (DE),A			;     
		LD A,(HL)			;  14-15
		INC HL				;  FDI 
		INC D				;       
		BIT 1,H
		CALL NZ,LOAD1SECFDI		;            
		LD B,A				;  14-15  
		LD A,(HL)			; 16-23     
		RL B
		RLA
		RL B
		RLA
		LD (DE),A			;     
		INC D
		REPT 4
		INC HL
		ENDM
		BIT 1,H
		CALL NZ,LOAD1SECFDI		;            
		LD B,(HL)			;    
		LD A,B
		AND A
		JP Z,LOADFDI_ERR
		INC HL
		INC HL
LOADFDI4	INC HL
		BIT 1,H
		CALL NZ,LOAD1SECFDI		;            
		LDI				;  
		DEC DE
		INC C
		INC D
		BIT 1,H
		CALL NZ,LOAD1SECFDI		;            
		LD A,(HL)			;  
		EX DE,HL
		LD (HL),1
		AND A
		JR Z,LOADFDI2
		LD (HL),2
		DEC A
		JR Z,LOADFDI2
		LD (HL),4
		DEC A
		JR Z,LOADFDI2
		LD (HL),8
LOADFDI2	EX DE,HL
		INC D
		REPT 5
		INC HL
		ENDM
		DJNZ LOADFDI4			;  ,     
		LD D,0X40
		INC E
		DEC C
		JR NZ,LOADFDI3			;      
		DEC HL
		BIT 1,H
		CALL NZ,LOAD1SECFDI		;            
		PUSH HL
		LD A,H
		AND 1
		LD B,A
		LD C,L
		LD A,IXH
		ADD A,A
		ADD A,B
		LD B,A
		LD HL,(START_DATA)
		AND A
		SBC HL,BC
		EX DE,HL
		POP HL
		LD A,D
		AND A
		JR Z,LOADFDI7
LOADFDI8	INC H
		BIT 1,H
		CALL NZ,LOAD1SECFDI		;            
		DEC D
		JR NZ,LOADFDI8
LOADFDI7	ADD HL,DE
		BIT 1,H
		CALL NZ,LOAD1SECFDI		;            
		PUSH HL				;     
		LD DE,CPU3
		AND A
		SBC HL,DE
		LD (OSTAT_SECT),HL		;          
		LD B,H
		LD C,L
		LD HL,0X200
		SBC HL,BC
		LD (NACH_SECT),HL		;          
		POP HL				;   ,      
LOADFDI5	POP AF
		INC A
		PUSH BC
		LD BC,WIN_P5
		OUT (C),A
		POP BC
		PUSH AF
		BIT 1,H
		CALL NZ,LOAD1SECFDI
		EX AF,AF'
		LD DE,CPU5
		LD BC,(NACH_SECT)
		LDIR
		EX AF,AF'
		JP C,ELT5
		EX DE,HL
		LD A,0X1F
		CALL READ_FILE
		JP C,ELT5
		PUSH HL
		LD HL,CPU3
		PUSH HL
		LD A,1
		CALL READ_FILE
		POP HL
		POP DE
		LD BC,(OSTAT_SECT)
		LDIR
		JR LOADFDI5

LOADFDI_ERR	POP AF
		EVOPORT WIN_P5,PAGE_RAMDISK
		CALL FORMAT_RAMDISK
		LD IX,LDFDIERROR
		SCF
		RET

FORMAT_RAMDISK	EVOPORT WIN_P5,PAGE_DATARAMD
		LD HL,CPU5
		LD DE,CPU5+1
		LD BC,0XFFF
		LD (HL),L
		LDIR
		LD HL,DSKINFO
		LD DE,CPU5+0X8E1
		LD BC,DSK_END-DSKINFO
		LDIR
		JP ILD_IMG3

;    FDI
LOAD1SECFDI	PUSH HL
		PUSH BC
		PUSH DE
		PUSH AF
		LD HL,CPU3
		LD A,1
		CALL READ_FILE
		INC IXH
		POP AF
		POP DE
		POP BC
		POP HL
		RES 1,H
		RET

OPEN_MOUNT	LD A,(RREG_A)
OPEN_MOUNT_INT	LD (MOUNT_SYM),A
		LD IYL,INTERNAL
		EVOPORT WIN_P5,PAGE_MOUNTER	;     
		LD A,(MOUNT_SYM)		;   
		ADD A,HIGH (BUF_PATHMOUNT+CPU5)
		LD D,A
		LD E,0				;DE=    
		LD A,(SETDVOL)			;  
		ADD A,HIGH (TEK_BUFPATH+CPU5)+4
		LD H,A
		LD L,E				;HL=    
		LD BC,0X100
		PUSH DE
		LDIR				; 
		CALL READ_DIR			;   
		POP DE
		PUSH HL
OPENMOUNT05	LD A,(DE)
		INC DE
		AND A
		JR NZ,OPENMOUNT05
		DEC DE
		LD BC,0X8FF
OPENMOUNT01	LDI
		LD A,(HL)
		CP "!"
		JR C,OPENMOUNT03
		DJNZ OPENMOUNT01
		LD A,"."
		LD (DE),A
		INC DE
		JR OPENMOUNT06

OPENMOUNT03	INC HL
		DJNZ OPENMOUNT03
		DEC HL
		LD A,"."
		LD (DE),A
		INC DE
OPENMOUNT06	LD B,3
OPENMOUNT02	LDI
		LD A,(HL)
		CP "!"
		JR C,OPENMOUNT04
		DJNZ OPENMOUNT02
OPENMOUNT04	XOR A
		LD (DE),A
		POP HL
		LD A,(MOUNT_SYM)
		PUSH HL
		LD B,A
		RRCA
		RRCA
		LD E,A
		LD D,HIGH (MOUNT_DRIVES)
		LD IXH,D
		LD IXL,E			;IX=   
		LD (IX+0X28),B
		LD BC,0X20
		LDIR
		LD A,(IX+0X28)
		LD D,A
		INC A
		LD H,%11110111
OPENMOUNT1	RLC H
		DEC A
		JR NZ,OPENMOUNT1
		IF NEWDOS=1
		CALL RD_DRIVE_INDOS
		ELSE
		IN A,(VG_MEM)
		ENDIF
		AND H
		LD L,A
		LD A,H
		CPL
		OR L
		IF NEWDOS=1
		CALL WR_DRIVE_INDOS		;   
		ELSE
		OUT (VG_MEM),A
		ENDIF
		EVOPORT WIN_P5,PAGE_MOUNTER	;     
		POP HL
;HL=   
IOPEN_MOUNT	LD DE,FILE_EXT
		CALL CP_EXT
		LD (IX+0X21),C			;   
		CALL ICOM_DEV
		DB _KOL_VOL
		LD (IX+0X23),A			; 
		LD (IX+0X24),D			;  
		LD L,D
		LD H,0
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		LD DE,BUF_TABLVOL		;   
		ADD HL,DE
		LD A,(HL)
		CP SDZ
		LD HL,COMSDZ
		JR Z,OPENMOUNT6
		CP SDG
		LD HL,COMSDG
		JR Z,OPENMOUNT6
		LD HL,COMHDDN
OPENMOUNT6	LD (IX+0X25),L
		LD (IX+0X26),H			;   

		LD E,(IX+0X1D)
		LD D,(IX+0X1E)
		LD L,(IX+0X1F)			;LDE= /256
		LD A,(BYTSSEC)
		LD H,A
OPENMOUNT3	SRL L
		RR D
		RR E
		RRCA
		JR NC,OPENMOUNT3		;LDE= 
;A00  (256 ) -> 500  (512 )
;=1 	1280/8 	=8
;=2 	640/4 	=4
;=4 	320/2 	=2
;=8 	160 	=1
;=16 	80 		=1
;=32 	40 		=1
;=64 	20 		=1
;=128 	10 		=1
		LD IY,1<<8+INTERNAL
		LD A,H
		CP 8
		JR NC,OPENMOUNT5
		LD IYH,8
		RRCA
		JR C,OPENMOUNT5
		LD IYH,4
		RRCA
		JR C,OPENMOUNT5
		LD IYH,2
OPENMOUNT5	LD A,IYH
		LD (IX+0X22),A			; 
		LD A,(IX+0X28)			;  
		ADD A,A
		ADD A,A
		ADD A,HIGH (MOUNT_CLS+CPU5)
		LD H,A
		LD L,0
		LD C,(IX+0X14)
		LD B,(IX+0X15)
		LD E,(IX+0X1A)
		LD D,(IX+0X1B)
		JR OPENMOUNT9

OPENMOUNT7	DEC IYH
		JR NZ,OPENMOUNT8
OPENMOUNT9	LD A,(IX+0X22)			; 
		LD IYH,A
		LD (HL),E
		INC H
		LD (HL),D
		INC H
		LD (HL),C
		INC H
		LD (HL),B
		DEC H
		DEC H
		DEC H
		INC L
OPENMOUNT8	PUSH HL
		CALL RDFATZP
		CALL LST_CLS
		POP HL
		JR NC,OPENMOUNT7
		RET

FILE_EXT	DZ "TRD"

;HL= /
;D= , E= +1
;A=  (BIT 7=0-, =1-)
RDWR_MOUNT	LD A,(RREG_A)
		AND 3
		RRCA
		RRCA
		LD IXL,A
		LD IXH,HIGH (MOUNT_DRIVES)	;IX=  A-D
		LD IYL,INTERNAL
		EVOPORT WIN_P5,PAGE_FATVARS
		LD A,(SETDVOL)
		ADD A,HIGH (CPU5+BUF_ALLVOL)+4
		LD D,A
		LD E,0
		LD HL,BUF_TEKVOL
		LD BC,0X100
		LDIR
		LD A,(IX+0X24)
		ADD A,HIGH (CPU5+BUF_ALLVOL)+4
		LD H,A
		LD L,0
		LD DE,BUF_TEKVOL
		LD BC,0X100
		LDIR
		EVOPORT WIN_P5,PAGE_MOUNTER
		LD HL,(GO_DEV)
		LD (IX+0X27),L
		LD (IX+0X28),H
		LD L,(IX+0X25)
		LD H,(IX+0X26)
		LD (GO_DEV),HL
		LD HL,(RREG_E)
		DEC L
		LD E,L
		LD L,H
		LD H,0
		LD D,H
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,HL
		ADD HL,DE			;HL=    (256 )
		PUSH HL
		LD A,(BYTSSEC)
RDWRMOUNT1	SRL H
		RR L
		RRCA
		JR NC,RDWRMOUNT1		;HL= 
;0XA00  (256 ) -> 0X500  (512 )
;=1 	1280/8 	=8
;=2 	640/4 	=4
;=4 	320/2 	=2
;=8 	160 	=1
;=16 	80 		=1
;=32 	40 		=1
;=64 	20 		=1
;=128 	10 		=1
		LD A,L
		EX AF,AF'
		LD A,(IX+0X22)
		DEC A
		JR Z,RDWRMOUNT6
		INC A
		ADD HL,HL
RDWRMOUNT2	SRL H
		RR L
		RRCA
		JR NC,RDWRMOUNT2
RDWRMOUNT6	LD A,(RREG_A)			; 
		ADD A,A
		ADD A,A
		ADD A,HIGH (MOUNT_CLS+CPU5)
		LD H,A
		LD E,(HL)
		INC H
		LD D,(HL)
		INC H
		LD C,(HL)
		INC H
		LD B,(HL)			;BCDE= 
		LD A,(IX+0X22)			; 
		DEC A
		JR Z,RDWRMOUNT3			;  =1
		LD B,A				;    
		EX AF,AF'
		AND B				;A=   
		LD B,(HL)			;  
		JR Z,RDWRMOUNT3
RDWRMOUNT4	PUSH AF
		CALL RDFATZP
		POP AF
		DEC A
		JR NZ,RDWRMOUNT4
RDWRMOUNT3	CALL REALSEC			;     
		POP HL
		PUSH HL
		SRL L
		LD A,(BYTSSEC)
		DEC A
		JR Z,RDWRMOUNT5
		AND L
		LD L,A
		LD H,0
		ADD HL,DE
		EX DE,HL
		LD HL,0
		ADC HL,BC
		LD B,H
		LD C,L
RDWRMOUNT5	CALL LOADLST			;   
		POP BC
		LD A,C
		AND 1
		ADD A,H
		LD H,A
		LD BC,CPU3
		ADD HL,BC			;    1
		CALL LDIR_SEC
		LD A,(RREG_A)			;  
		AND 0X80
		CALL NZ,LSTSAVE
		LD HL,RREG_H
		INC (HL)
		LD L,(IX+0X27)
		LD H,(IX+0X28)
		LD (GO_DEV),HL
		EVOPORT WIN_P5,PAGE_FATVARS
		LD A,(SETDVOL)
		ADD A,HIGH (CPU5+BUF_ALLVOL)+4
		LD H,A
		LD DE,BUF_TEKVOL
		LD BC,0X100
		LD L,C
		LDIR
		LD (LSTLOAD+2),HL
		RET

LDIR_SEC	EX DE,HL			;    DE
		LD HL,(RREG_L)			;HL=   
		CALL RESETUP_WINS		;  
		ADD HL,BC			;  /
		EX DE,HL			;HL= , DE=   
		LD A,(RREG_A-CPU5)		;  
		AND 0X80
		JR Z,LDIRSEC6
		EX DE,HL			;    
LDIRSEC6	LD BC,0X100
		LDIR
		LD HL,CPU5
		ADD HL,SP
		LD SP,HL			;     2
		ATMPORT WIN_A3,0X7F		;     3   7FFD
		LD B,HIGH (WIN_A6)
		OUT (C),A			;     2   7FFD
		XOR A
		LD B,HIGH (WIN_P6)
		OUT (C),A			;     2
		RET

TXT_MOUNTER	DZ "IMAGE.MNT"			;         TRD
ETXT_MOUNTER

FIND_MOUNTED	LD A,(KOLDVOL)
		AND A
		JR NZ,FINDMNTD09
		LD IX,MOUNT_DRIVES
		LD DE,0X40
		LD B,4
FINDMNTD10	LD (IX+0),D
		ADD IX,DE
		DJNZ FINDMNTD10
		RET

FINDMNTD09	LD A,(SETDVOL)
		LD (SAVE_TEK_VOL),A		;  
		CALL SAVE_FNDVOL		;   
		LD H,CMOS_BYTE_01
		PCALL READCMOS,P_ADDONS
		LD A,L
		AND AUTOMOUNT			;     IMAGE.MNT
		JP Z,FINDMNTD01
;  IMAGE.MNT  
		LD IYL,INTERNAL
		LD A,(KOLDVOL)
		DEC A				;     
		CALL SET_VOL_MNT		;   
		LD HL,(ROOTCLS)
		LD (TEK_DIR),HL
		LD HL,(ROOTCLS+2)
		LD (TEK_DIR+2),HL		;    
		CALL INIRTSC			;    ROOTDIR
		LD HL,TXT_MOUNTER
		LD DE,BUF_256
		LD BC,ETXT_MOUNTER-TXT_MOUNTER
		LDIR				;    
		CALL ICOM_FAT
		DB _FIND_NAME
		JP C,FINDMNTD01			; IMAGE.MNT  
; 
		CALL OPEN_FILE			; 
		LD HL,FILE_SRC+0X1F
		LD A,(HL)
		DEC HL
		OR (HL)
		JP NZ,FINDMNTD01		;     512 
		DEC HL
		LD A,(HL)
		CP 2
		JP NC,FINDMNTD01		;     512 
		LD A,1				;  1    
		LD B,(HL)
		DEC HL
		LD C,(HL)			;    
		PUSH BC				;   
		LD IYL,INTERNAL
		LD HL,OFFSET_BUFSYM+CPU6
		PUSH HL
		CALL READ_FILE			; 1   
		EVOPORT WIN_P5,PAGE_MOUNTER
		POP HL
		POP BC
		ADD HL,BC
		LD (HL),0			;     0
		SBC HL,BC
FINDMNTD04	LD A,(HL)
		AND A
		JR Z,FINDMNTD01			;   
		INC HL
		CP "!"
		JR C,FINDMNTD04			;         
		DEC HL
		PUSH HL
		INC HL
		CP "a"				;    
		JR C,FINDMNTD06			;     "A"
		CP 'z'+1
		JR NC,FINDMNTD06		; "Z"
		AND 0XDF			;     ,  
FINDMNTD06	SUB "E"				;FAT     "E"
		JR C,FINDMNTD02
		EXX
		LD C,A				;    
		LD A,(KOLDVOL)			;  
		CP C
		EXX
		JR C,FINDMNTD02
		LD A,(HL)
		INC HL
		CP ":"				; 
		JR NZ,FINDMNTD02
		LD A,(HL)
		INC HL
		CP "/"				; 
		JR NZ,FINDMNTD02
FINDMNTD07	LD A,(HL)
		INC HL
		CP "!"
		JR NC,FINDMNTD07		;  
		LD A,(HL)			;    
		INC HL
		CP "a"
		JR C,FINDMNTD08
		CP 'z'+1
		JR NC,FINDMNTD08
		AND 0XDF			;   ,  
FINDMNTD08	SUB "A"
		JR C,FINDMNTD02
		CP 4
		JR NC,FINDMNTD02
		EXX
		LD B,A				;    
		EXX
		LD A,(HL)
		INC HL
		CP ":"
		JR NZ,FINDMNTD02
		LD D,H
		LD E,L
		EX (SP),HL
		EX DE,HL
		SCF
		SBC HL,DE
		LD B,H
		LD C,L
		EX DE,HL
		EXX
		LD A,B
		EXX
		ADD A,HIGH (BUF_PATHMOUNT+CPU5)
		LD D,A
		LD E,0				;DE=     
		DEC BC
		DEC BC
		LDIR				; 
		XOR A
		LD (DE),A			;   
		POP HL
		JR FINDMNTD04

FINDMNTD02	LD A,(HL)
		INC HL
		AND A
		JR Z,FINDMNTD01
		CP " "
		JR NC,FINDMNTD02
		POP DE
		JR FINDMNTD04

FINDMNTD01	EVOPORT WIN_P5,PAGE_MOUNTER
		LD BC,0X400
		LD IX,MOUNT_DRIVES		;   
FINDMNTD2	PUSH BC
		CALL FINDMNTD0			;    
		JR NC,FINDMNT5
		LD (IX+0),0			;   ,    
FINDMNT5	LD BC,0X40
		ADD IX,BC			;   
		POP BC
		DJNZ FINDMNTD2
		LD A,IYH
		AND 0XF0			; 4   
		LD D,A
		IF NEWDOS=1
		CALL RD_DRIVE_INDOS
		ELSE
		IN A,(VG_MEM)
		ENDIF
		AND 0X0F			; 4     
		OR D				;   
		IF NEWDOS=1
		CALL WR_DRIVE_INDOS		;    EVO-DOS
		ELSE
		OUT (VG_MEM),A
		ENDIF
		LD A,(SAVE_TEK_VOL)
		PUSH IY
		LD IYL,INTERNAL
		CALL SET_VOL_MNT		;   
		CALL INIRTSC			;  
		POP IY
		JP LOAD_FNDVOL			;    

;      
FINDMNTD0	CALL FINDMNTD1
		EX AF,AF'
		LD A,IXL
		AND 0XC0
		RLCA
		RLCA				;   
		INC A
		LD B,A
		LD A,%11110111
		RLCA
		DJNZ $-1			;   
		LD C,A				;
		CPL
		LD B,A				;
		LD A,IYH
		AND C				;   
		LD IYH,A
		EX AF,AF'
		RET C				;    
		LD A,IYH
		OR B				;    
		LD IYH,A
		RET

;  
STORE_PATH	PUSH AF
		EVOPORT WIN_P5,PAGE_MOUNTER
		LD A,(SETDVOL)			;  
		ADD A,HIGH (TEK_BUFPATH+CPU5)+4	;+   
		LD H,A
		LD DE,BUF_256
		LD BC,0X100
		LD L,C
		LDIR
STOREPATH1	POP AF
		RET

;  
RESTORE_PATH	PUSH AF
		EVOPORT WIN_P5,PAGE_MOUNTER
		LD A,(SETDVOL)			;  
		ADD A,HIGH (TEK_BUFPATH+CPU5)+4	;+   
		LD D,A
		LD HL,BUF_256
		LD BC,0X100
		LD E,C
		LDIR
RESTOREPATH1	POP AF
		RET

;  
FINDMNTD1	LD A,IXL
		RLCA
		RLCA
		AND 3
		ADD A,HIGH (BUF_PATHMOUNT+CPU5)
		LD D,A
		LD E,0				;DE=     
		LD A,(DE)			;     
		INC DE
		INC DE
		INC DE
		AND A
		SCF
		RET Z
		SUB "E"				;   
		PUSH DE
		CALL SET_VOL_MNT		;   
		CALL STORE_PATH			;  
		LD HL,(ROOTCLS)
		LD (TEK_DIR),HL
		LD HL,(ROOTCLS+2)
		LD (TEK_DIR+2),HL		;   
		CALL INIRTSC			;  
		EVOPORT WIN_P5,PAGE_MOUNTER
		POP HL
		CALL FNDBUF			;  
FINDMNTD13	PUSH HL
		CALL POSTF02			;     0
		CALL FINDMNTD3			;   
		POP DE
		JP C,RESTORE_PATH		;  
		LD BC,0X0B
		ADD HL,BC
		LD A,(HL)
		SBC HL,BC
		AND 0X10
		JR Z,FINDMNTD23			;   ,   
		PUSH DE
		CALL ENTER_DIR			;   
		CALL POSTF02			;     0
		POP HL
		LD A,(HL)
		AND A
		SCF
		JP Z,RESTORE_PATH		;  
		CALL FNDBUF			;   
		JR FINDMNTD13			; 

FINDMNTD23	LD A,IXL
		RLCA
		RLCA
		LD E,IXL
		LD D,IXH			;DE=  
		LD (IX+0X28),A			;  
		PUSH HL
		LD BC,0X20
		LDIR
		POP HL
		PUSH IY
		CALL IOPEN_MOUNT
		POP IY
		XOR A
		JP RESTORE_PATH			;  

FINDMNTD4	CALL NXTLEGZ
FINDMNTD3	PUSH HL
		LD DE,FB_EXT
		CALL ICMP_NAME
		POP HL
		RET Z
		LD A,(HL)
		AND A
		JR NZ,FINDMNTD4
		SCF
		RET

REST_NAMELOAD	LD HL,NAME_RAMDISK-CPU6
		LD DE,(RREG_L)
		LD BC,0X20
		JP LDIR_BYTES

STOR_NAMELOAD	LD HL,(RREG_L)
		LD DE,NAME_RAMDISK-CPU6
		LD BC,0X20
		JP LDIR_BYTES

GET_MOUNTED	EVOPORT WIN_P5,PAGE_MOUNTER
		LD HL,MOUNT_DRIVES-CPU6
		LD DE,(RREG_L)
		LD BC,0X100
		JP LDIR_BYTES

CMP_DRIVE	EVOPORT WIN_P5,PAGE_MOUNTER
		LD A,(RREG_A)
		AND 3
		RLCA
		RLCA
		LD L,A
		LD H,HIGH (MOUNT_DRIVES)
		LD A,(HL)
		AND A				;  ,
		SCF				;  
		JR Z,CMP_DRIVE1
		XOR A
CMP_DRIVE1	PUSH AF
		POP HL
		LD (RREG_F),HL
		RET

CLOSEMOUNT	EVOPORT WIN_P5,PAGE_MOUNTER
		LD A,(RREG_A)
		LD B,A
		RRCA
		RRCA
		LD L,A
		LD H,HIGH (MOUNT_DRIVES)
		LD (HL),0
		LD A,B
		ADD A,HIGH (BUF_PATHMOUNT+CPU5)
		LD H,A
		LD L,0
		LD (HL),L
		INC B
		LD A,%11110111
		RLCA
		DJNZ $-1
		LD B,A
		IF NEWDOS=1
		CALL RD_DRIVE_INDOS
		ELSE
		IN A,(VG_MEM)
		ENDIF
		AND B
		IF NEWDOS=1
WR_DRIVE_INDOS	PUSH BC
		PUSH HL
		LD BC,WIN_P2
		LD HL,PAGE_EVODOS
		OUT (C),L
		LD (CPU6+DRIVE_IN_DOS),A
		OUT (C),H
		POP HL
		POP BC
		ELSE
		OUT (VG_MEM),A
		ENDIF
		RET

		IF NEWDOS=1
RD_DRIVE_INDOS	PUSH BC
		PUSH HL
		EVOPORT WIN_P2,PAGE_EVODOS
		LD HL,(CPU6+DRIVE_IN_DOS)
		XOR A
		OUT (C),A
		LD A,L
		POP HL
		POP BC
		RET
		ENDIF

;   
;HL=  
FNDBUF		LD BC,0X0802
		LD DE,FB_EXT
FNDBUF4		LD A,(HL)
		INC HL
		CP "."
		JR Z,FNDBUF2
		CP "/"
		JR Z,FNDBUF5
		LD (DE),A
		INC DE
		DJNZ FNDBUF4
		LD A,(HL)
		AND A
		RET Z
		INC HL
		JR FNDBUF3

FNDBUF5		LD A,C
		AND A
		RET Z
FNDBUF2		LD A,B
		AND A
		JR Z,FNDBUF3
		LD A," "
		LD (DE),A
		INC DE
		DJNZ $-2
FNDBUF3		LD B,3
		DEC C
		DEC HL
		LD A,(HL)
		CP "/"
		JR Z,FNDBUF4
		INC HL
		JR FNDBUF4
